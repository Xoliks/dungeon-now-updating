from reapermc:api import
    listener

from ../api import
    get_nbt
    add_custom_model_data
    on_entity_item_tick

id = ctx.meta.item.diamond_upgrade

nbt = get_nbt("diamond_upgrade", id, "true")

add_custom_model_data("diamond_upgrade", id, "diamond")


loot_table ../item/diamond_upgrade {
    "pools": [
        {
            "rolls": 1,
            "entries": [
                {
                    "type": "minecraft:item",
                    "name": "minecraft:diamond",
                    "functions": [
                        {
                            "function": "minecraft:set_name",
                            "entity": "this",
                            "name": {
                                "translate": "Diamond Upgrade Template",
                                "color": "#FFAA00",
                                "italic": false
                            }
                        },
                        {
                            "function": "minecraft:set_lore",
                            "entity": "this",
                            "lore": [
                                {
                                    "translate": "Throw this item and an item that has",
                                    "color": "#555555",
                                    "italic": false
                                },
                                {
                                    "translate": "lower tier than diamond to upgrade",
                                    "color": "#555555",
                                    "italic": false
                                },
                                {
                                    "translate": "it to diamond tier.",
                                    "color": "#555555",
                                    "italic": false
                                }
                            ]
                        },
                        {
                            "function": "minecraft:set_nbt",
                            "tag": nbt
                        }
                    ]
                }
            ]
        }
    ]
}

@listener(on_entity_item_tick)
def entity_item_tick():
    if score #dnl.item_entity_id dnl.int matches id run function ./diamond_upgrade/main:
        scoreboard players set #dnl.upgraded dnl.boolean 0
        as @e[type=item,distance=..0.5] at @s if score @s dnl.item_entity_tier matches 1.. run function ./diamond_upgrade/detect_upgrade_item:
            scoreboard players operation #dnl.weapon_id dnl.int = @s dnl.item_entity
            scoreboard players set #dnl.upgraded dnl.boolean 1
            data remove storage dnl:item_entity/enchantments Item.tag.Enchantments
            data modify storage dnl:item_entity/enchantments Item.tag.Enchantments set from entity @s Item.tag.Enchantments
            function ./diamond_upgrade/listener
            as @e[type=item,limit=1,distance=..0.5,nbt={Age:0s}] run data modify entity @s Item.tag.Enchantments set from storage dnl:item_entity/enchantments Item.tag.Enchantments
            playsound minecraft:block.smithing_table.use master @p ~ ~ ~ 1 1 1
            particle minecraft:enchanted_hit ~ ~ ~ 0 0 0 1 20 normal
            particle minecraft:totem_of_undying ~ ~ ~ 0 0 0 1 20 normal
            kill @s
        if score #dnl.upgraded dnl.boolean matches 1 run function ./diamond_upgrade/consume:
            store result score #dnl.count dnl.int run data get entity @s Item.Count 1
            scoreboard players operation #dnl.count dnl.int -= #dnl.1 dnl.constant
            if score #dnl.count dnl.int matches 0 run kill @s
            if score #dnl.count dnl.int matches 1.. store result entity @s Item.Count byte 1 run scoreboard players get #dnl.count dnl.int 