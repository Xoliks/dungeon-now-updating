from ../api import
    on_entity_mob_tick
    on_marker_core
    add_summon
    set_bossbar
    remove_bossbar
    add_death_event
    rng
    outcome

from reapermc:api import
    listener

add_summon poison_eyes cave_spider boss 8 7 0 flame {
    DeathLootTable:"dnl:entity/poison_eyes/death",
    Tags:["dnl.boss","dnl.mob","dnl.tick","dnl.poison_eyes"],
    PersistenceRequired:1b,
    Health:180f,
    Attributes:[{Name:generic.max_health,Base:180},{Name:generic.movement_speed,Base:0.45},{Name:generic.attack_damage,Base:5.0}]
}

add_death_event("poison_eyes")

@listener(on_entity_mob_tick)
def entity_mob_tick():
    if entity @s[tag=dnl.poison_eyes] run function ./poison_eyes/main

@listener(on_marker_core)
def marker_core():
    if entity @s[tag=dnl.poison_eyes] run function ./poison_eyes/core/main:
        if entity @s[tag=dnl.reset] run function ./poison_eyes/core/reset:
            tag @s remove dnl.reset
            fill ~10 ~5 ~10 ~-10 ~-2 ~-10 minecraft:air replace minecraft:cobweb
            function ../spawner/poison_eyes

function ./poison_eyes/main:
    ### Init ###
    if entity @s[tag=!dnl.poison_eyes.init] run function ./poison_eyes/init:
        tag @s add dnl.poison_eyes.init

        scoreboard players operation #dnl.poison_eyes.id dnl.eid = @s dnl.eid
        as @e[type=marker,tag=dnl.core,tag=dnl.poison_eyes,distance=..30] run function ./poison_eyes/core/copy:
            scoreboard players operation @s dnl.eid = #dnl.poison_eyes.id dnl.eid
        unless entity @e[type=marker,tag=dnl.core,tag=dnl.poison_eyes,distance=..30] run function ./poison_eyes/core/setup:
            summon marker ~ ~ ~ {Tags:["dnl.marker","dnl.core","dnl.poison_eyes","dnl.new_marker"]}
            scoreboard players operation @e[type=marker,tag=dnl.new_marker] dnl.eid = @s dnl.eid
            tag @e[type=marker,tag=dnl.new_marker] remove dnl.new_marker
    
    ### Boss Bar ###
    set_bossbar poison_eyes purple notched_10 180 sphere 30 0 0
    store result score @s dnl.health run data get entity @s Health 1

    ### Arena ###
    scoreboard players reset #dnl.poison_eyes.reset dnl.boolean
    scoreboard players operation #dnl.poison_eyes.id dnl.eid = @s dnl.eid
    if score #dnl.1s dnl.tick matches 1 run function ./poison_eyes/core/tick:
        tag @s add dnl.this
        as @e[type=marker,tag=dnl.core,tag=dnl.poison_eyes] if score @s dnl.eid = #dnl.poison_eyes.id dnl.eid run function ./poison_eyes/core/check:
            at @s unless entity @a[distance=..30] run function ./poison_eyes/core/trigger_reset
            if entity @s[distance=30..] at @s run tp @e[type=cave_spider,tag=dnl.poison_eyes,tag=dnl.this] ~ ~ ~
        tag @s remove dnl.this
    if score #dnl.poison_eyes.reset dnl.boolean matches 1 run function ./poison_eyes/reset:
        data modify entity @s DeathLootTable set value "minecraft:empty"
        remove_bossbar this
        particle poof ~ ~ ~ 0.3 1 0.3 0.0001 5 normal
        tp @s ~ ~-100 ~
        kill @s
    
    ### Main ###
    scoreboard players remove @s dnl.timer 1

    ### Moves ###
    if entity @s[tag=dnl.poison_eyes.web_armor] run function ./poison_eyes/move/web_armor
    if entity @s[tag=dnl.poison_eyes.poison_aura] run function ./poison_eyes/move/poison_aura
    if entity @s[tag=dnl.poison_eyes.small_web_shield] run function ./poison_eyes/move/small_web_shield
    if entity @s[tag=dnl.poison_eyes.large_web_shield] run function ./poison_eyes/move/large_web_shield

    ### Phase 1 ###
    if score @s dnl.timer matches ..0 if score @s dnl.health matches 121..180 run function ./poison_eyes/phase_1/init:
        scoreboard players set @s dnl.timer 200
        rng 1 2
        outcome 1:
            tag @s add dnl.poison_eyes.web_armor
        outcome 2:
            tag @s add dnl.poison_eyes.poison_aura
    
    ### Phase 2 ###
    if score @s dnl.timer matches ..0 if score @s dnl.health matches 61..120 run function ./poison_eyes/phase_2/init:
        scoreboard players set @s dnl.timer 180
        rng 1 3
        outcome 1:
            tag @s add dnl.poison_eyes.web_armor
        outcome 2:
            tag @s add dnl.poison_eyes.poison_aura
        outcome 3:
            tag @s add dnl.poison_eyes.small_web_shield
    
    ### Phase 3 ###
    if score @s dnl.timer matches ..0 if score @s dnl.health matches ..60 run function ./poison_eyes/phase_3/init:
        scoreboard players set @s dnl.timer 160
        rng 1 3
        outcome 1:
            tag @s add dnl.poison_eyes.web_armor
        outcome 2:
            tag @s add dnl.poison_eyes.poison_aura
        outcome 3:
            tag @s add dnl.poison_eyes.large_web_shield

function ./poison_eyes/move/web_armor:
    if score @s dnl.timer matches 80 run effect give @s minecraft:slowness 3 100 true
    if score @s dnl.timer matches ..80 run particle minecraft:item string ~ ~ ~ 0.2 0.2 0.2 0.00001 20
    if score @s dnl.timer matches ..20 if block ~ ~ ~ minecraft:air run setblock ~ ~ ~ minecraft:cobweb
    if score @s dnl.timer matches 0 run tag @s remove dnl.poison_eyes.web_armor

function ./poison_eyes/move/poison_aura:
    if score @s dnl.timer matches 80 run effect give @s minecraft:slowness 3 100 true
    if score @s dnl.timer matches ..80 run particle minecraft:item green_dye ~ ~ ~ 0.5 0.5 0.5 0.00001 20
    if score @s dnl.timer matches ..20 run summon area_effect_cloud ~ ~ ~ {Radius:3f,Duration:40,Age:40,WaitTime:40,Color:5149489,Effects:[{Id:19b,Amplifier:1b,Duration:40}]}
    if score @s dnl.timer matches 0 run tag @s remove dnl.poison_eyes.poison_aura

function ./poison_eyes/move/small_web_shield:
    if score @s dnl.timer matches 60 run effect give @s minecraft:slowness 4 100 true
    if score @s dnl.timer matches ..60 run particle minecraft:block cobweb ~ ~ ~ 0.5 0.5 0.5 1 20
    if score @s dnl.timer matches 0 run particle minecraft:block cobweb ~ ~ ~ 1 1 1 1 50
    if score @s dnl.timer matches 0 run fill ~-1 ~-1 ~-1 ~1 ~1 ~1 minecraft:cobweb replace minecraft:air
    if score @s dnl.timer matches 0 run tag @s remove dnl.poison_eyes.small_web_shield

function ./poison_eyes/move/large_web_shield:
    if score @s dnl.timer matches 60 run effect give @s minecraft:slowness 4 100 true
    if score @s dnl.timer matches ..60 run particle minecraft:block cobweb ~ ~ ~ 0.5 0.5 0.5 1 20
    if score @s dnl.timer matches 0 run particle minecraft:block cobweb ~ ~ ~ 2 2 2 1 50
    if score @s dnl.timer matches 0 run fill ~-2 ~-2 ~-2 ~2 ~2 ~2 minecraft:cobweb replace minecraft:air
    if score @s dnl.timer matches 0 run tag @s remove dnl.poison_eyes.large_web_shield

function ./poison_eyes/core/trigger_reset:
    tag @s add dnl.reset
    scoreboard players set #dnl.poison_eyes.reset dnl.boolean 1

function ./poison_eyes/death:
    remove_bossbar item
    advancement grant @a[distance=..32] only dnl:story/structures/crawlers_lair/poison_eyes
    as @e[type=marker,tag=dnl.core,tag=dnl.poison_eyes] if score @s dnl.eid = #dnl.death_entity_id dnl.eid at @s run function ./poison_eyes/core/death:
        setblock ~ ~ ~ minecraft:chest{LootTable:"dnl:entity/poison_eyes/reward"} 
        kill @s
    
loot_table ./poison_eyes/reward {
    "type": "minecraft:entity",
    "pools": [
        {
            "rolls": 1,
            "entries": [
                {
                    "type": "minecraft:loot_table",
                    "name": "dnl:item/forbidden_berries"
                }
            ]
        },
        {
            "rolls": 1,
            "entries": [
                {
                    "type": "minecraft:item",
                    "name": "minecraft:spider_eye",
                    "functions": [
                        {
                            "function": "minecraft:set_count",
                            "count": {
                                "min": 4,
                                "max": 8
                            }
                        }
                    ]
                }
            ]
        },
        {
            "rolls": 1,
            "entries": [
                {
                    "type": "minecraft:item",
                    "name": "minecraft:string",
                    "functions": [
                        {
                            "function": "minecraft:set_count",
                            "count": {
                                "min": 4,
                                "max": 8
                            }
                        }
                    ]
                }
            ]
        }
    ]
}
