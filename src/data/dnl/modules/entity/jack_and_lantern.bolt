from ../api import
    on_technical_mob_tick
    on_entity_mob_tick
    add_summon
    add_death_event

from reapermc:api import
    listener

add_summon jack_and_lantern cat none 16 0 0 flame {
    Tags:["dnl.mob","dnl.tick","dnl.jack"],
    CustomName:'{"translate":"Jack","color":"gold","italic":false}',
    PersistenceRequired:1b,
    Health:100f,
    Attributes:[{Name:generic.max_health,Base:100}],
    HandDropChances:[0.000F,0.000F],
    CatType:10,
    CollarColor:1b,
    Passengers:[{
        id:"minecraft:bat",
        Tags:["dnl.mob","dnl.tick","dnl.lantern"],
        CustomName:'{"translate":"Lantern","color":"gold","italic":false}',
        PersistenceRequired:1b,
        Health:100f,
        Attributes:[{Name:generic.max_health,Base:100}]
    }]
}

@listener(on_entity_mob_tick)
def entity_mob_tick():
    if entity @s[tag=dnl.lantern] run function ./lantern/main:
        particle wax_on ~ ~0.3 ~ 0.2 0.2 0.2 1 1 normal

add_summon jack_and_lantern_spawn marker none 16 0 0 flame {
    Tags:["dnl.mob","dnl.tick","dnl.jack_and_lantern_spawn"]
}

@listener(on_technical_mob_tick)
def technical_mob_tick():
    if entity @s[tag=dnl.jack_and_lantern_spawn] run function ./jack_and_lantern_spawn/main

function ./jack_and_lantern_spawn/main:
    ### Init ###
    unless entity @s[tag=dnl.jack_and_lantern_spawn.init] run function ./jack_and_lantern_spawn/init:
        tag @s add dnl.jack_and_lantern_spawn.init
        setblock ~ ~ ~ minecraft:jukebox

    ### Repair ###
    if block ~ ~ ~ minecraft:air run function ./jack_and_lantern_spawn/repair

    ### Play Music ###
    if entity @s[tag=dnl.jack_and_lantern_spawn.play_music] run function ./jack_and_lantern_spawn/playing

    ### Stop Music ###
    if entity @s[tag=dnl.jack_and_lantern_spawn.stop_music] run function ./jack_and_lantern_spawn/stop

function ./jack_and_lantern_spawn/repair:
    scoreboard players reset #dnl.jack_and_lantern_spawn.creative dnl.boolean
    if entity @a[distance=..10,gamemode=creative] run function ./jack_and_lantern_spawn/repair/creative:
        kill @s
        scoreboard players set #dnl.jack_and_lantern_spawn.creative dnl.boolean 1
    unless score #dnl.jack_and_lantern_spawn.creative dnl.boolean matches 1 run function ./jack_and_lantern_spawn/repair/non_creative:
        playsound minecraft:entity.experience_orb.pickup master @a ~ ~ ~ 1 0.5 0
        tellraw @a[distance=..10] {"translate":"‚óè You cannot mine this block","color":"red","italic":false}
        setblock ~ ~ ~ jukebox replace
        tag @s remove dnl.jack_and_lantern_spawn.play_music
        scoreboard players reset @s dnl.timer
        kill @e[type=minecraft:item,distance=..1,nbt={Age:0s}]

function ./jack_and_lantern_spawn/playing:
    scoreboard players add @s dnl.timer 1
    if score @s dnl.timer matches 45 run title @a[distance=..32] title {"translate":".","color":"blue"}
    if score @s dnl.timer matches 85 run title @a[distance=..32] title {"translate":". .","color":"blue"}
    if score @s dnl.timer matches 125 run title @a[distance=..32] title {"translate":". . .","color":"blue"}
    if score @s dnl.timer matches 175 run title @a[distance=..32] title {"translate":".","color":"blue"}
    if score @s dnl.timer matches 215 run title @a[distance=..32] title {"translate":". .","color":"blue"}
    if score @s dnl.timer matches 255 run title @a[distance=..32] title {"translate":". . .","color":"blue"}
    if score @s dnl.timer matches 305 run title @a[distance=..32] title {"translate":".","color":"blue"}
    if score @s dnl.timer matches 345 run title @a[distance=..32] title {"translate":". .","color":"blue"}
    if score @s dnl.timer matches 385 run title @a[distance=..32] title {"translate":". . .","color":"blue"}
    if score @s dnl.timer matches 435 run title @a[distance=..32] title {"translate":".","color":"blue"}
    if score @s dnl.timer matches 475 run title @a[distance=..32] title {"translate":". .","color":"blue"}
    if score @s dnl.timer matches 515 run title @a[distance=..32] title {"translate":". . .","color":"blue"}
    if score @s dnl.timer matches 610 run title @a[distance=..32] title {"translate":"Dungeon Now Loading","color":"gold"}
    if score @s dnl.timer matches 610 run title @a[distance=..32] subtitle {"translate":"Minecraft Datapack","color":"blue"}
    if score @s dnl.timer matches 750 run title @a[distance=..32] title {"translate":"Hex","color":"gold"}
    if score @s dnl.timer matches 750 run title @a[distance=..32] subtitle {"translate":"Creator","color":"blue"}
    if score @s dnl.timer matches 910 run title @a[distance=..32] title {"translate":"Thank you!!","color":"gold"}
    if score @s dnl.timer matches 910 run title @a[distance=..32] subtitle {"translate":"for Playing","color":"blue"}
    if score @s dnl.timer matches 1000 run particle minecraft:cloud ~ ~ ~ 0.5 1 0.5 0.0000001 30
    if score @s dnl.timer matches 1000 positioned ~ ~1 ~ run function ../summon/jack_and_lantern 
    if score @s dnl.timer matches 1000 run kill @s

function ./jack_and_lantern_spawn/stop:
    tag @s remove dnl.jack_and_lantern_spawn.stop_music
    tag @s remove dnl.jack_and_lantern_spawn.play_music
    scoreboard players reset @s dnl.timer
