from ../api import
    on_entity_mob_tick
    on_marker_core
    add_summon
    set_bossbar
    remove_bossbar
    add_death_event
    on_entity_hit_target

from reapermc:api import
    listener

add_summon hunter_ghost skeleton boss 8 7 0 flame {
    DeathLootTable:"dnl:entity/hunter_ghost/death",
    Tags:["dnl.boss","dnl.mob","dnl.tick","dnl.hunter_ghost"],
    Glowing:1b,
    Silent:1b,
    PersistenceRequired:1b,
    Health:200.0f,
    Attributes:[{Name:generic.max_health,Base:200}],
    HandItems:[{id:"minecraft:stone_sword",Count:1b,tag:{Enchantments:[{id:"minecraft:sharpness",lvl:3s},{id:"minecraft:knockback",lvl:2s}]}},{}],
    HandDropChances:[0.000F,0.000F],
    ArmorItems:[{},{id:"minecraft:leather_leggings",Count:1b,tag:{Unbreakable:1b,Enchantments:[{}]}},{},{id:"minecraft:leather_helmet",Count:1b,tag:{Unbreakable:1b,Enchantments:[{}]}}],
    ArmorDropChances:[0.000F,0.000F,0.000F,0.000F],
    ActiveEffects:[{Id:14b,Amplifier:0b,Duration:32766}]
    }

add_death_event("hunter_ghost")

@listener(on_entity_mob_tick)
def entity_mob_tick():
    if entity @s[tag=dnl.hunter_ghost] run function ./hunter_ghost/main

@listener(on_entity_hit_target)
def on_entity_hit_target():
    if entity @s[tag=dnl.hunter_ghost] if score @s dnl.phase matches 2.. run function ./hunter_ghost/hurt:
        function ./hunter_ghost/teleport
        if entity @s[tag=dnl.hunter_ghost.has_bow] at @a[distance=..5] run function ./hunter_ghost/switch_to_strong_sword
        if entity @s[tag=dnl.hunter_ghost.has_sword] at @a[distance=5..] run function ./hunter_ghost/switch_to_strong_bow

@listener(on_marker_core)
def marker_core():
    if entity @s[tag=dnl.hunter_ghost] run function ./hunter_ghost/core/main:
        if entity @s[tag=dnl.reset] run function ./hunter_ghost/core/reset:
            tag @s remove dnl.reset
            function ../spawner/hunter_ghost

function ./hunter_ghost/main:
    ### Init ###
    if entity @s[tag=!dnl.hunter_ghost.init] run function ./hunter_ghost/init:
        tag @s add dnl.hunter_ghost.init
        tag @s add dnl.hunter_ghost.has_sword
        scoreboard players set @s dnl.phase 1

        scoreboard players operation #dnl.hunter_ghost.id dnl.eid = @s dnl.eid
        as @e[type=marker,tag=dnl.core,tag=dnl.hunter_ghost,distance=..30] run function ./hunter_ghost/core/copy:
            scoreboard players operation @s dnl.eid = #dnl.hunter_ghost.id dnl.eid
        unless entity @e[type=marker,tag=dnl.core,tag=dnl.hunter_ghost,distance=..30] run function ./hunter_ghost/core/setup:
            summon marker ~ ~ ~ {Tags:["dnl.marker","dnl.core","dnl.hunter_ghost","dnl.new_marker"]}
            scoreboard players operation @e[type=marker,tag=dnl.new_marker] dnl.eid = @s dnl.eid
            tag @e[type=marker,tag=dnl.new_marker] remove dnl.new_marker
    
    ### Boss Bar ###
    set_bossbar hunter_ghost purple notched_10 200 sphere 30 0 0
    store result score @s dnl.health run data get entity @s Health 1
    
    ### Arena ###
    scoreboard players reset #dnl.hunter_ghost.reset dnl.boolean
    scoreboard players operation #dnl.hunter_ghost.id dnl.eid = @s dnl.eid
    if score #dnl.1s dnl.tick matches 1 run function ./hunter_ghost/core/tick:
        tag @s add dnl.this
        as @e[type=marker,tag=dnl.core,tag=dnl.hunter_ghost] if score @s dnl.eid = #dnl.hunter_ghost.id dnl.eid run function ./hunter_ghost/core/check:
            at @s unless entity @a[distance=..30] run function ./hunter_ghost/core/trigger_reset
            if entity @s[distance=30..] at @s run tp @e[type=skeleton,tag=dnl.hunter_ghost,tag=dnl.this] ~ ~ ~
        tag @s remove dnl.this
    if score #dnl.hunter_ghost.reset dnl.boolean matches 1 run function ./hunter_ghost/reset:
        data modify entity @s DeathLootTable set value "minecraft:empty"
        remove_bossbar this
        particle poof ~ ~ ~ 0.3 1 0.3 0.0001 5 normal
        tp @s ~ ~-100 ~
        kill @s

    ### Phase 1 ###
    if score @s dnl.health matches 101..200 run function ./hunter_ghost/phase_1/main:
        if score #dnl.1s dnl.tick matches 1 run function ./hunter_ghost/phase_1/switch_weapon:
            if entity @s[tag=dnl.hunter_ghost.has_bow] at @a[distance=..4] run function ./hunter_ghost/switch_to_sword
            if entity @s[tag=dnl.hunter_ghost.has_sword] at @a[distance=4..] run function ./hunter_ghost/switch_to_bow
    
    ### Phase 2 ###
    if score @s dnl.health matches ..100 run function ./hunter_ghost/phase_2/main:
        if score @s dnl.phase matches 1 run function ./hunter_ghost/phase_2/init:
            scoreboard players set @s dnl.phase 2
            function ./hunter_ghost/teleport
            if entity @s[tag=dnl.hunter_ghost.has_bow] run function ./hunter_ghost/switch_to_strong_bow
            if entity @s[tag=dnl.hunter_ghost.has_sword] run function ./hunter_ghost/switch_to_strong_sword
        scoreboard players add @s dnl.timer 1
        if score @s dnl.timer matches 100.. run function ./hunter_ghost/teleport
        if score #dnl.1s dnl.tick matches 1 run function ./hunter_ghost/phase_2/switch_weapon:
            if entity @s[tag=dnl.hunter_ghost.has_bow] at @a[distance=..4] run function ./hunter_ghost/switch_to_strong_sword
            if entity @s[tag=dnl.hunter_ghost.has_sword] at @a[distance=4..] run function ./hunter_ghost/switch_to_strong_bow

    ### Phase 3 ###
    if score @s dnl.health matches ..12 run particle minecraft:soul ~ ~1 ~ 0.3 0.5 0.3 0.01 1

function ./hunter_ghost/teleport:
    spreadplayers ~ ~ 1 4 false @s
    playsound entity.enderman.teleport master @a[distance=..16] ~ ~ ~ 1 2 1
    scoreboard players reset @s dnl.timer

function ./hunter_ghost/switch_to_sword:
    item replace entity @s weapon.mainhand with minecraft:stone_sword{Enchantments:[{id:"minecraft:sharpness",lvl:3s},{id:"minecraft:knockback",lvl:2s}]}
    attribute @s minecraft:generic.movement_speed base set 0.3 
    tag @s add dnl.hunter_ghost.has_sword
    tag @s remove dnl.hunter_ghost.has_bow

function ./hunter_ghost/switch_to_bow:
    item replace entity @s weapon.mainhand with minecraft:bow{Enchantments:[{id:"minecraft:power",lvl:3s}]}
    attribute @s minecraft:generic.movement_speed base set 0.25
    tag @s remove dnl.hunter_ghost.has_sword
    tag @s add dnl.hunter_ghost.has_bow

function ./hunter_ghost/switch_to_strong_sword:
    item replace entity @s weapon.mainhand with minecraft:stone_sword{Enchantments:[{id:"minecraft:sharpness",lvl:3s},{id:"minecraft:knockback",lvl:2s}]}
    attribute @s minecraft:generic.movement_speed base set 0.4 
    tag @s add dnl.hunter_ghost.has_sword
    tag @s remove dnl.hunter_ghost.has_bow

function ./hunter_ghost/switch_to_strong_bow:
    item replace entity @s weapon.mainhand with minecraft:bow{Enchantments:[{id:"minecraft:power",lvl:3s}]}
    attribute @s minecraft:generic.movement_speed base set 0.4
    tag @s remove dnl.hunter_ghost.has_sword
    tag @s add dnl.hunter_ghost.has_bow

function ./hunter_ghost/core/trigger_reset:
    tag @s add dnl.reset
    scoreboard players set #dnl.hunter_ghost.reset dnl.boolean 1

function ./hunter_ghost/death:
    remove_bossbar item
    scoreboard players reset #dnl.hunter_ghost.killed_with_broken_bow dnl.boolean
    advancement grant @a[distance=..32] only dnl:story/structures/hunters_grave/hunters_ghost
    if entity @a[tag=dnl.hunter_ghost.killed_with_broken_bow] run function ./hunter_ghost/killed_with_broken_bow:
        advancement grant @a[distance=..32] only dnl:story/structures/hunters_grave/heir_of_hunter
        tag @a[tag=dnl.hunter_ghost.killed_with_broken_bow] remove dnl.hunter_ghost.killed_with_broken_bow
        playsound entity.wolf.howl ambient @a ~ ~ ~ 1.0 1 1
        scoreboard players set #dnl.hunter_ghost.killed_with_broken_bow dnl.boolean 1
    as @e[type=marker,tag=dnl.core,tag=dnl.hunter_ghost] if score @s dnl.eid = #dnl.death_entity_id dnl.eid at @s run function ./hunter_ghost/check_spawn:
        setblock ~ ~ ~ minecraft:chest{LootTable:"dnl:entity/hunter_ghost/reward"}
        if score #dnl.hunter_ghost.killed_with_broken_bow dnl.boolean matches 1 run function ./hunter_ghost/spawn_gray:
            positioned ~ ~1 ~ run function ../summon/gray
            particle minecraft:cloud ~ ~ ~ 0.5 0.5 0.5 0.01 10
        kill @s

loot_table ./hunter_ghost/reward {
    "type": "minecraft:entity",
    "pools": [
        {
            "rolls": 1,
            "entries": [
                {
                    "type": "minecraft:loot_table",
                    "name": "dnl:item/old_letter",
                    "functions": [
                        {
                            "function": "minecraft:set_count",
                            "count": {
                                "min": 3,
                                "max": 5
                            }
                        }
                    ]
                }
            ]
        },
        {
            "rolls": 1,
            "entries": [
                {
                    "type": "minecraft:loot_table",
                    "name": "dnl:item/forbidden_berries"
                }
            ]
        }
    ]
}



    