from ../api import
    on_entity_mob_tick
    on_marker_core
    add_summon
    set_bossbar
    remove_bossbar
    add_death_event
    rng
    outcome
    assign_lid
    setup_core
from reapermc:api import
    listener

add_summon cleric_z zombie_villager boss 64 0 0 flame {
    DeathLootTable:"dnl:entity/cleric_z/death",
    Tags:["dnl.boss","dnl.mob","dnl.tick","dnl.cleric_z"],
    PersistenceRequired:1b,
    Health:200f,
    Attributes:[{Name:generic.max_health,Base:200}],
    VillagerData:{level:99,profession:"minecraft:cleric",type:"minecraft:plains"},
    Offers:{
        Recipes:[
            {
                buy:{id:"minecraft:emerald",Count:7b},
                buyB:{id:"minecraft:melon_slice",Count:1b},
                sell:{id:"minecraft:potion",Count:1b,tag:{Potion:"minecraft:strong_healing"}}
            },
            {
                buy:{id:"minecraft:emerald",Count:20b},
                buyB:{id:"minecraft:melon_slice",Count:3b},
                sell:{id:"minecraft:potion",Count:3b,tag:{Potion:"minecraft:strong_healing"}}
            },
            {
                buy:{id:"minecraft:emerald",Count:7b},
                buyB:{id:"minecraft:spider_eye",Count:1b},
                sell:{id:"minecraft:splash_potion",Count:1b,tag:{Potion:"minecraft:weakness"}}
            },
            {
                buy:{id:"minecraft:emerald",Count:16b},
                sell:{id:"minecraft:golden_apple",Count:1b}
            },
            {
                buy:{id:"minecraft:emerald",Count:64b},
                sell:{id:"minecraft:chest",Count:1b,tag:{display:{Name:'{"translate":"Cleric Loot Box","color":"gold","italic":false}'},BlockEntityTag:{LootTable:"dnl:entity/cleric_z_villager/loot_box"}}}
            }
        ]
    }
}

add_death_event("cleric_z")

@listener(on_entity_mob_tick)
def entity_mob_tick():
    if entity @s[tag=dnl.cleric_z] run function ./cleric_z/main

@listener(on_marker_core)
def marker_core():
    if entity @s[tag=dnl.cleric_z] run function ./cleric_z/core/main:
        ### Start ###
        if entity @s[tag=dnl.start] if score #dnl.1s dnl.tick matches 1 run function ./cleric_z/core/start:
            scoreboard players operation #dnl.cleric_z.core.lid dnl.lid = @s dnl.lid
            unless entity @a[distance=..30] run tag @s add dnl.reset
            as @e[type=zombie_villager,tag=dnl.cleric_z] if score @s dnl.lid = #dnl.cleric_z.core.lid dnl.lid unless entity @s[distance=..30] run tp @s ~ ~ ~

        ### Reset ###
        if entity @s[tag=dnl.reset] run function ./cleric_z/core/reset:
            scoreboard players operation #dnl.cleric_z.core.lid dnl.lid = @s dnl.lid
            as @e[type=zombie_villager,tag=dnl.cleric_z,distance=..30] if score @s dnl.lid = #dnl.cleric_z.core.lid dnl.lid run tag @s add dnl.reset
            as @e[type=zombie_villager,tag=dnl.potion_zombie_villager,distance=..30] if score @s dnl.lid = #dnl.cleric_z.core.lid dnl.lid run tp @s ~ -1000 ~
            tag @s remove dnl.start
            tag @s remove dnl.reset
            function ../spawner/cleric_z

function ./cleric_z/main:
    ### Init ###
    if entity @s[tag=!dnl.cleric_z.init] run function ./cleric_z/init:
        tag @s add dnl.cleric_z.init
        assign_lid
        scoreboard players set @s dnl.phase 1
        loot replace entity @s armor.head loot dnl:item/cleric_z_banner
        setup_core cleric_z
    
    ### Boss Bar ###
    set_bossbar cleric_z purple notched_10 200 sphere 30 0 0
    store result score @s dnl.health run data get entity @s Health 1

    ### Reset ###
    if entity @s[tag=dnl.reset] run function ./cleric_z/reset:
        data modify entity @s DeathLootTable set value "minecraft:empty"
        remove_bossbar this
        particle poof ~ ~ ~ 0.3 1 0.3 0.0001 5 normal
        tp @s ~ -1000 ~
        kill @s

    ### Conversion ###
    if score #dnl.1s dnl.tick matches 1 run scoreboard players reset #dnl.cleric_z.conversioning dnl.boolean
    if score #dnl.1s dnl.tick matches 1 store success score #dnl.cleric_z.conversioning dnl.boolean run data get entity @s ConversionPlayer
    if score #dnl.cleric_z.conversioning dnl.boolean matches 1 store result score #dnl.cleric_z.conversion_time dnl.int run data get entity @s ConversionTime
    if score #dnl.cleric_z.conversioning dnl.boolean matches 1 if score #dnl.cleric_z.conversion_time dnl.int matches ..1 run function ./cleric_z/converted:
        remove_bossbar this
        advancement grant @a[distance=..32] only dnl:story/structures/cleric_hideout/villager_doctor
        scoreboard players operation #dnl.cleric_z.lid dnl.lid = @s dnl.lid
        as @e[type=marker,tag=dnl.core,tag=dnl.cleric_z] if score @s dnl.lid = #dnl.cleric_z.lid dnl.lid run kill @s

    ### Transformation ###
    if score #dnl.1s dnl.tick matches 1 as @e[type=#dnl:undeads,tag=!dnl.mob,distance=..64] at @s run function ./cleric_z/transform_zombie:
        particle minecraft:cloud ~ ~ ~ 0.5 0.5 0.5 0.01 10
        function ../summon/potion_zombie_villager
        tp @s ~ -1000 ~

    ### Phase 1 ###
    if score @s dnl.phase matches 1 if score @s dnl.health matches ..150 run function ./cleric_z/phase_1/transition:
        scoreboard players add @s dnl.phase 1
        function ../summon/potion_zombie_villager
        function ../summon/potion_zombie_villager
        function ../summon/potion_zombie_villager
        scoreboard players operation #dnl.cleric_z.lid dnl.lid = @s dnl.lid
        as @e[type=zombie_villager,tag=dnl.potion_zombie_villager,tag=!dnl.potion_zombie_villager.assign_lid] unless score @s dnl.lid matches 0.. run function ./cleric_z/potion_zombie_villager_init
    
    ### Phase 2 ###
    if score @s dnl.phase matches 2 if score @s dnl.health matches ..100 run function ./cleric_z/phase_2/transition:
        fill ~-12 ~-12 ~-12 ~12 ~12 ~12 minecraft:air replace black_stained_glass_pane
        playsound block.glass.break ambient @a ~ ~ ~ 1.0 1 1

        scoreboard players add @s dnl.phase 1
        function ../summon/potion_zombie_villager
        function ../summon/potion_zombie_villager
        function ../summon/potion_zombie_villager
        scoreboard players operation #dnl.cleric_z.lid dnl.lid = @s dnl.lid
        as @e[type=zombie_villager,tag=dnl.potion_zombie_villager,tag=!dnl.potion_zombie_villager.assign_lid] unless score @s dnl.lid matches 0.. run function ./cleric_z/potion_zombie_villager_init

    
    ### Phase 3 ###
    if score @s dnl.phase matches 3 if score @s dnl.health matches ..50 run function ./cleric_z/phase_3/transition:
        scoreboard players add @s dnl.phase 1
        function ../summon/potion_zombie_villager
        function ../summon/potion_zombie_villager
        function ../summon/potion_zombie_villager
        scoreboard players operation #dnl.cleric_z.lid dnl.lid = @s dnl.lid
        as @e[type=zombie_villager,tag=dnl.potion_zombie_villager,tag=!dnl.potion_zombie_villager.assign_lid] unless score @s dnl.lid matches 0.. run function ./cleric_z/potion_zombie_villager_init


function ./cleric_z/potion_zombie_villager_init:
    tag @s add dnl.potion_zombie_villager.assign_lid
    scoreboard players operation @s dnl.lid = #dnl.cleric_z.lid dnl.lid


function ./cleric_z/death:
    remove_bossbar item
    advancement grant @a[distance=..32] only dnl:story/structures/cleric_hideout/cleric_z
    kill @e[type=zombie_villager,tag=dnl.potion_zombie_villager,distance=..64]
    as @e[type=marker,tag=dnl.core,tag=dnl.cleric_z] if score @s dnl.lid = #dnl.death_entity_lid dnl.lid at @s run function ./cleric_z/core/death:
        setblock ~ ~ ~ minecraft:chest{LootTable:"dnl:entity/cleric_z/reward"}
        kill @s

loot_table ./cleric_z/reward {
    "type": "minecraft:entity",
    "pools": [
        {
            "rolls": 1,
            "entries": [
                {
                    "type": "minecraft:loot_table",
                    "name": "dnl:item/forbidden_apple"
                }
            ]
        }
    ]
}
