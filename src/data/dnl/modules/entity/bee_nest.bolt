from ../api import
    on_technical_mob_tick
    add_summon

from reapermc:api import
    listener

add_summon bee_nest marker none 16 0 0 flame {
    Tags:["dnl.marker","dnl.mob","dnl.tick","dnl.bee_nest"]
}

@listener(on_technical_mob_tick)
def technical_mob_tick():
    if entity @s[tag=dnl.bee_nest] run function ./bee_nest/main

function ./bee_nest/main:    
    if entity @s[tag=!dnl.bee_nest.destroyed] run function ./bee_nest/linked:
        ### Init ###
        if entity @s[tag=!dnl.bee_nest.init] run function ./bee_nest/init:
            tag @s add dnl.bee_nest.init
            if block ~ ~ ~ air run setblock ~ ~ ~ minecraft:bee_nest
        
        ### Main ###
        particle dust 1.000 0.800 0.251 1 ~ ~ ~ 0.4 0.4 0.4 1 10 normal
        
        if entity @s[tag=dnl.bee_nest.summon_angry_bee] run function ./bee_nest/summon_angry_bee:
            tag @s remove dnl.bee_nest.summon_angry_bee
            if entity @s[tag=!dnl.bee_nest.spawned] if block ~ ~1 ~ minecraft:air positioned ~ ~1 ~ run function ./bee_nest/spawnable
            if entity @s[tag=!dnl.bee_nest.spawned] if block ~1 ~1 ~ minecraft:air positioned ~1 ~1 ~ run function ./bee_nest/spawnable
            if entity @s[tag=!dnl.bee_nest.spawned] if block ~-1 ~1 ~ minecraft:air positioned ~-1 ~1 ~ run function ./bee_nest/spawnable
            if entity @s[tag=!dnl.bee_nest.spawned] if block ~ ~1 ~1 minecraft:air positioned ~ ~1 ~1 run function ./bee_nest/spawnable
            if entity @s[tag=!dnl.bee_nest.spawned] if block ~ ~1 ~-1 minecraft:air positioned ~ ~1 ~-1 run function ./bee_nest/spawnable
            tag @s remove dnl.bee_nest.spawned

        if entity @s[tag=dnl.bee_nest.barrier] run function ./bee_nest/barrier:
            tag @s remove dnl.bee_nest.barrier
            particle minecraft:composter ~ ~ ~ 1.5 1.5 1.5 0.01 10 normal @a
            playsound minecraft:block.grass.place block @a[distance=..16] ~ ~ ~ 1 0.7 1
            fill ~1 ~ ~ ~-1 ~ ~ minecraft:jungle_leaves replace air
            fill ~ ~ ~1 ~ ~ ~-1 minecraft:jungle_leaves replace air
            fill ~ ~1 ~ ~ ~-1 ~ minecraft:jungle_leaves replace air

        if block ~ ~ ~ air run function ./bee_nest/destroyed:
            function dnl:summon/angry_bee
            as @e[type=marker,tag=dnl.bee_swarm,distance=..15] run tag @s add dnl.bee_swarm.hurt
            tag @s add dnl.bee_nest.destroyed

function ./bee_nest/spawnable:
    function dnl:summon/angry_bee
    playsound minecraft:block.beehive.exit block @a[distance=..16] ~ ~ ~ 1 1 1
    tag @s add dnl.bee_nest.spawned

function ./bee_nest/reset:
    unless block ~ ~ ~ bee_nest run setblock ~ ~ ~ bee_nest
    tag @s remove dnl.bee_nest.destroyed
