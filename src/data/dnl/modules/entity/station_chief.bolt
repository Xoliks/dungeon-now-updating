from ../api import
    on_entity_mob_tick
    on_marker_core
    add_summon
    set_bossbar
    remove_bossbar
    add_death_event

from reapermc:api import
    listener

add_summon station_chief pillager boss 16 0 0 flame {
    DeathLootTable:"dnl:entity/station_chief/death",
    Tags:["dnl.boss","dnl.mob","dnl.tick","dnl.station_chief"],
    PersistenceRequired:1b,
    Health:360f,
    Attributes:[{Name:generic.max_health,Base:360}],
    HandDropChances:[0.000F,0.000F]
}

add_death_event("station_chief")

@listener(on_entity_mob_tick)
def entity_mob_tick():
    if entity @s[tag=dnl.station_chief] run function ./station_chief/main

@listener(on_marker_core)
def marker_core():
    if entity @s[tag=dnl.station_chief] run function ./station_chief/core/main:
        if entity @s[tag=dnl.reset] run function ./station_chief/core/reset:
            tag @s remove dnl.reset
            tag @s remove dnl.overlayed
            unless entity @e[type=marker,tag=dnl.spawner,tag=dnl.station_chief,distance=..1] function ../spawner/station_chief
        if entity @s[tag=dnl.overlayed] run function ./station_chief/core/overlayed:
            if score #dnl.1s dnl.tick matches 1 unless entity @e[type=marker,tag=dnl.core,tag=dnl.station_chief_ravager,distance=..1] at @s run function ./station_chief/reward:
                setblock ~ ~ ~ chest{LootTable:"dnl:entity/station_chief/reward"}
                advancement grant @a[distance=..32] only dnl:story/structures/railway_outpost/station_chief
                kill @s

function ./station_chief/main:
    ### Init ###
    if entity @s[tag=!dnl.station_chief.init] run function ./station_chief/init:
        tag @s add dnl.station_chief.init

        scoreboard players operation #dnl.station_chief.id dnl.eid = @s dnl.eid
        as @e[type=marker,tag=dnl.core,tag=dnl.station_chief,distance=..30] run function ./station_chief/core/copy:
            scoreboard players operation @s dnl.eid = #dnl.station_chief.id dnl.eid
        unless entity @e[type=marker,tag=dnl.core,tag=dnl.station_chief,distance=..30] run function ./station_chief/core/setup:
            summon marker ~ ~ ~ {Tags:["dnl.marker","dnl.core","dnl.station_chief","dnl.new_marker"]}
            scoreboard players operation @e[type=marker,tag=dnl.new_marker] dnl.eid = @s dnl.eid
            tag @e[type=marker,tag=dnl.new_marker] remove dnl.new_marker
    
    ### Boss Bar ###
    set_bossbar station_chief purple notched_10 360 sphere 30 0 0
    store result score @s dnl.health run data get entity @s Health 1

    ### Arena ###
    scoreboard players reset #dnl.station_chief.reset dnl.boolean
    scoreboard players operation #dnl.station_chief.id dnl.eid = @s dnl.eid
    if score #dnl.1s dnl.tick matches 1 run function ./station_chief/core/tick:
        tag @s add dnl.this
        as @e[type=marker,tag=dnl.core,tag=dnl.station_chief] if score @s dnl.eid = #dnl.station_chief.id dnl.eid run function ./station_chief/core/check:
            at @s unless entity @a[distance=..30] run function ./station_chief/core/trigger_reset
            if entity @s[distance=30..] at @s run tp @e[type=pillager,tag=dnl.station_chief,tag=dnl.this] ~ ~ ~
        tag @s remove dnl.this
    if score #dnl.station_chief.reset dnl.boolean matches 1 run function ./station_chief/reset:
        data modify entity @s DeathLootTable set value "minecraft:empty"
        remove_bossbar this
        particle poof ~ ~ ~ 0.3 1 0.3 0.0001 5 normal
        tp @s ~ ~-100 ~
        kill @s

    ### Main ###
    if score #dnl.1s dnl.tick matches 1 run function ./station_chief/weapon:
        if score @s dnl.health matches 271..360 run item replace entity @s weapon.mainhand with minecraft:crossbow{Enchantments:[{id:"minecraft:quick_charge",lvl:2s}],ChargedProjectiles:[{id:"minecraft:arrow",Count:1b},{id:"minecraft:arrow",Count:1b},{id:"minecraft:arrow",Count:1b}],Charged:1b}
        if score @s dnl.health matches 181..270 run item replace entity @s weapon.mainhand with minecraft:crossbow{Enchantments:[{id:"minecraft:quick_charge",lvl:3s}],ChargedProjectiles:[{id:"minecraft:tipped_arrow",Count:1b,tag:{CustomPotionEffects:[{Id:17b,Amplifier:2b,Duration:300}],CustomPotionColor:5797459}},{id:"minecraft:tipped_arrow",Count:1b,tag:{CustomPotionEffects:[{Id:17b,Amplifier:2b,Duration:300}],CustomPotionColor:5797459}},{id:"minecraft:tipped_arrow",Count:1b,tag:{CustomPotionEffects:[{Id:17b,Amplifier:2b,Duration:300}],CustomPotionColor:5797459}}],Charged:1b}
        if entity @s[tag=dnl.station_chief.riding] if score @s dnl.health matches 91..180 run item replace entity @s weapon.mainhand with minecraft:crossbow{Enchantments:[{id:"minecraft:quick_charge",lvl:3s}],ChargedProjectiles:[{id:"minecraft:tipped_arrow",Count:1b,tag:{CustomPotionEffects:[{Id:17b,Amplifier:2b,Duration:300},{Id:19b,Amplifier:3b,Duration:60}],CustomPotionColor:5149489}},{id:"minecraft:tipped_arrow",Count:1b,tag:{CustomPotionEffects:[{Id:17b,Amplifier:2b,Duration:300},{Id:19b,Amplifier:3b,Duration:60}],CustomPotionColor:5149489}},{id:"minecraft:tipped_arrow",Count:1b,tag:{CustomPotionEffects:[{Id:17b,Amplifier:2b,Duration:300},{Id:19b,Amplifier:3b,Duration:60}],CustomPotionColor:5149489}}],Charged:1b}
        if entity @s[tag=dnl.station_chief.riding] if score @s dnl.health matches 0..90 run item replace entity @s weapon.mainhand with minecraft:crossbow{Enchantments:[{id:"minecraft:quick_charge",lvl:4s}],ChargedProjectiles:[{id:"minecraft:tipped_arrow",Count:1b,tag:{CustomPotionEffects:[{Id:9b,Amplifier:0b,Duration:200},{Id:17b,Amplifier:2b,Duration:300},{Id:19b,Amplifier:3b,Duration:60}],CustomPotionColor:5578058}},{id:"minecraft:tipped_arrow",Count:1b,tag:{CustomPotionEffects:[{Id:9b,Amplifier:0b,Duration:200},{Id:17b,Amplifier:2b,Duration:300},{Id:19b,Amplifier:3b,Duration:60}],CustomPotionColor:5578058}},{id:"minecraft:tipped_arrow",Count:1b,tag:{CustomPotionEffects:[{Id:9b,Amplifier:0b,Duration:200},{Id:17b,Amplifier:2b,Duration:300},{Id:19b,Amplifier:3b,Duration:60}],CustomPotionColor:5578058}}],Charged:1b}

    if entity @s[tag=!dnl.station_chief.riding] if score @s dnl.health matches ..180 run function ./station_chief/start_riding:
        scoreboard players add @s dnl.timer 1
        if score @s dnl.timer matches 1 run item replace entity @s weapon.offhand with minecraft:goat_horn
        if score @s dnl.timer matches 20 run playsound event.raid.horn ambient @a[distance=..16] ~ ~ ~ 100.0 1 1
        if score @s dnl.timer matches 60 run function ./station_chief/summon_ravager:
            remove_bossbar this
            particle minecraft:cloud ~ ~2 ~ 1 1 1 0.001 50
            function ../summon/station_chief_ravager
            scoreboard players operation #dnl.station_chief.id dnl.eid = @s dnl.eid
            as @e[type=pillager,tag=dnl.station_chief.new,limit=1,sort=nearest] run function ./station_chief/replace_boss:
                tag @s remove dnl.station_chief.new
                scoreboard players operation @s dnl.eid = #dnl.station_chief.id dnl.eid
            data modify entity @s DeathLootTable set value "minecraft:empty"
            tp @s ~ ~-1000 ~
            kill @s


function ./station_chief/core/trigger_reset:
    tag @s add dnl.reset
    scoreboard players set #dnl.station_chief.reset dnl.boolean 1

function ./station_chief/death:
    remove_bossbar item
    as @e[type=marker,tag=dnl.core,tag=dnl.station_chief] if score @s dnl.eid = #dnl.death_entity_id dnl.eid run tag @s add dnl.overlayed

loot_table ./station_chief/reward {
    "type": "minecraft:entity",
    "pools": [
        {
            "rolls": 1,
            "entries": [
                {
                    "type": "minecraft:loot_table",
                    "name": "dnl:item/forbidden_apple"
                }
            ]
        },
        {
            "rolls": 1,
            "entries": [
                {
                    "type": "minecraft:loot_table",
                    "name": "dnl:item/forbidden_berries"
                }
            ]
        }
    ]
}