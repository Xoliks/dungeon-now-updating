from ../api import
    on_entity_mob_tick
    on_entity_hurt_target
    on_entity_hurt_attacker
    on_marker_core
    add_summon
    set_bossbar
    remove_bossbar
    add_death_event
    assign_lid
    assign_equipment
from reapermc:api import
    listener

add_summon frost_watcher skeleton boss 64 0 0 flame {
    DeathLootTable:"dnl:entity/frost_watcher/death",
    Tags:["dnl.boss","dnl.mob","dnl.tick","dnl.frost_watcher"],
    PersistenceRequired:1b,
    Health:300f,
    Attributes:[{Name:generic.max_health,Base:300},{Name:generic.follow_range,Base:64},{Name:generic.knockback_resistance,Base:1},{Name:generic.movement_speed,Base:0.25}],
    HandDropChances:[0.000F,0.000F],
    ArmorItems:[{},{},{id:"minecraft:netherite_chestplate",Count:1b},{id:"minecraft:netherite_helmet",Count:1b}],
    ArmorDropChances:[0.000F,0.000F,0.000F,0.000F]
}

add_death_event("frost_watcher")

@listener(on_entity_mob_tick)
def entity_mob_tick():
    if entity @s[tag=dnl.frost_watcher] run function ./frost_watcher/main

@listener(on_marker_core)
def marker_core():
    if entity @s[tag=dnl.frost_watcher] run function ./frost_watcher/core/main:
        if entity @s[tag=dnl.reset] run function ./frost_watcher/core/reset:
            tag @s remove dnl.reset
            scoreboard players set @e[tag=dnl.frost_watcher_minion,distance=..64] dnl.timer 1200
            tp @e[type=stray,tag=dnl.frost_watcher_guard,distance=..64] ~ ~-1000 ~
            function ../spawner/frost_watcher

@listener(on_entity_hurt_attacker)
def on_entity_hurt_attacker():
    if entity @s[tag=dnl.frost_watcher] run scoreboard players set #dnl.frost_watcher.hurt_target dnl.boolean 1

@listener(on_entity_hurt_target)
def on_entity_hurt_target():
    if score #dnl.frost_watcher.hurt_target dnl.boolean matches 1 run function ./frost_watcher/hurt_target:
        scoreboard players reset #dnl.frost_watcher.hurt_target dnl.boolean



function ./frost_watcher/main:
    ### Init ###
    if entity @s[tag=!dnl.frost_watcher.init] run function ./frost_watcher/init:
        tag @s add dnl.frost_watcher.init
        scoreboard players set @s dnl.phase 1
        loot replace entity @s weapon.mainhand loot dnl:item/frost_watcher_bow
        assign_equipment mainhand set

        scoreboard players operation #dnl.frost_watcher.id dnl.eid = @s dnl.eid
        as @e[type=marker,tag=dnl.core,tag=dnl.frost_watcher,distance=..64] run function ./frost_watcher/core/copy:
            scoreboard players operation @s dnl.eid = #dnl.frost_watcher.id dnl.eid
        unless entity @e[type=marker,tag=dnl.core,tag=dnl.frost_watcher,distance=..64] run function ./frost_watcher/core/setup:
            summon marker ~ ~ ~ {Tags:["dnl.marker","dnl.core","dnl.frost_watcher","dnl.new_marker"]}
            scoreboard players operation @e[type=marker,tag=dnl.new_marker] dnl.eid = @s dnl.eid
            tag @e[type=marker,tag=dnl.new_marker] remove dnl.new_marker
    
    ### Boss Bar ###
    set_bossbar frost_watcher purple notched_10 300 sphere 64 0 0
    store result score @s dnl.health run data get entity @s Health 1

    ### Arena ###
    scoreboard players reset #dnl.frost_watcher.reset dnl.boolean
    scoreboard players operation #dnl.frost_watcher.id dnl.eid = @s dnl.eid
    if score #dnl.1s dnl.tick matches 1 run function ./frost_watcher/core/tick:
        tag @s add dnl.this
        as @e[type=marker,tag=dnl.core,tag=dnl.frost_watcher] if score @s dnl.eid = #dnl.frost_watcher.id dnl.eid run function ./frost_watcher/core/check:
            at @s unless entity @a[distance=..64] run function ./frost_watcher/core/trigger_reset
            if entity @s[distance=64..] at @s run tp @e[type=skeleton,tag=dnl.frost_watcher,tag=dnl.this] ~ ~ ~
        tag @s remove dnl.this
    if score #dnl.frost_watcher.reset dnl.boolean matches 1 run function ./frost_watcher/reset:
        data modify entity @s DeathLootTable set value "minecraft:empty"
        remove_bossbar this
        particle poof ~ ~ ~ 0.3 1 0.3 0.0001 5 normal
        tp @s ~ ~-100 ~
        kill @s

    if score @s dnl.health matches ..200 run function ./frost_watcher/phase_2:
        if score @s dnl.phase matches 1 run function ./frost_watcher/phase_2/init:
            item modify entity @s weapon.mainhand dnl:item/frost_watcher_bow/set_2
            scoreboard players add @s dnl.phase 1
            function ./frost_watcher/teleport_to_spawn

    if score @s dnl.health matches ..100 run function ./frost_watcher/phase_3:
        if score @s dnl.phase matches 2 run function ./frost_watcher/phase_3/init:
            item modify entity @s weapon.mainhand dnl:item/frost_watcher_bow/set_3
            scoreboard players add @s dnl.phase 1
            function ./frost_watcher/teleport_to_spawn
        if score @s dnl.phase matches 3 as @e[type=arrow,tag=!dnl.frost_watcher_bow.arrow,distance=2..4] at @s run function ./frost_watcher/barrier

function ./frost_watcher/teleport_to_spawn:
    tag @s add dnl.this
    as @e[type=marker,tag=dnl.core,tag=dnl.frost_watcher] if score @s dnl.eid = #dnl.frost_watcher.id dnl.eid at @s run function ./frost_watcher/teleport_and_spawn:
        tp @e[type=skeleton,tag=dnl.frost_watcher,tag=dnl.this] ~ ~ ~
        summon stray ~3 ~-3 ~ {Tags:["dnl.mob","dnl.frost_watcher_guard"],HandItems:[{id:"minecraft:bow",Count:1b,tag:{Enchantments:[{id:"minecraft:power",lvl:1s},{id:"minecraft:punch",lvl:1s}]}},{}],HandDropChances:[0.000F,0.000F],ArmorItems:[{},{},{id:"minecraft:iron_chestplate",Count:1b,tag:{Unbreakable:1b}},{id:"minecraft:diamond_helmet",Count:1b,tag:{Unbreakable:1b}}],ArmorDropChances:[0.000F,0.000F,0.000F,0.000F]}
        summon stray ~ ~-3 ~3 {Tags:["dnl.mob","dnl.frost_watcher_guard"],HandItems:[{id:"minecraft:bow",Count:1b,tag:{Enchantments:[{id:"minecraft:power",lvl:1s},{id:"minecraft:punch",lvl:1s}]}},{}],HandDropChances:[0.000F,0.000F],ArmorItems:[{},{},{id:"minecraft:iron_chestplate",Count:1b,tag:{Unbreakable:1b}},{id:"minecraft:diamond_helmet",Count:1b,tag:{Unbreakable:1b}}],ArmorDropChances:[0.000F,0.000F,0.000F,0.000F]}
        summon stray ~-3 ~-3 ~ {Tags:["dnl.mob","dnl.frost_watcher_guard"],HandItems:[{id:"minecraft:bow",Count:1b,tag:{Enchantments:[{id:"minecraft:power",lvl:1s},{id:"minecraft:punch",lvl:1s}]}},{}],HandDropChances:[0.000F,0.000F],ArmorItems:[{},{},{id:"minecraft:iron_chestplate",Count:1b,tag:{Unbreakable:1b}},{id:"minecraft:diamond_helmet",Count:1b,tag:{Unbreakable:1b}}],ArmorDropChances:[0.000F,0.000F,0.000F,0.000F]}
        summon stray ~ ~-3 ~-3 {Tags:["dnl.mob","dnl.frost_watcher_guard"],HandItems:[{id:"minecraft:bow",Count:1b,tag:{Enchantments:[{id:"minecraft:power",lvl:1s},{id:"minecraft:punch",lvl:1s}]}},{}],HandDropChances:[0.000F,0.000F],ArmorItems:[{},{},{id:"minecraft:iron_chestplate",Count:1b,tag:{Unbreakable:1b}},{id:"minecraft:diamond_helmet",Count:1b,tag:{Unbreakable:1b}}],ArmorDropChances:[0.000F,0.000F,0.000F,0.000F]}

    tag @s remove dnl.this

function ./frost_watcher/barrier:
    setblock ~ ~ ~ minecraft:packed_ice
    setblock ~ ~ ~ minecraft:air destroy
    kill @s

function ./frost_watcher/core/trigger_reset:
    tag @s add dnl.reset
    scoreboard players set #dnl.frost_watcher.reset dnl.boolean 1

function ./frost_watcher/death:
    remove_bossbar item
    advancement grant @a[distance=..32] only dnl:story/structures/watcher_camp/frost_watcher
    as @e[type=marker,tag=dnl.core,tag=dnl.frost_watcher] if score @s dnl.eid = #dnl.death_entity_id dnl.eid at @s run function ./frost_watcher/core/death:
        setblock ~ ~ ~ minecraft:chest{LootTable:"dnl:entity/frost_watcher/reward"}
        kill @s

loot_table ./frost_watcher/reward {
    "type": "minecraft:entity",
    "pools": [
        {
            "rolls": 1,
            "entries": [
                {
                    "type": "minecraft:loot_table",
                    "name": "dnl:item/forbidden_berries"
                }
            ]
        }
    ]
}

