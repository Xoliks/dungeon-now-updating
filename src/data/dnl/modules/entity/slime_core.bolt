from ../api import
    on_entity_mob_tick
    on_marker_core
    on_marker_skill
    add_summon
    set_bossbar
    remove_bossbar
    add_death_event
    rng
    outcome

from reapermc:api import
    listener

add_summon slime_core slime boss 8 7 0 flame {
    DeathLootTable:"dnl:entity/slime_core/death",
    Tags:["dnl.boss","dnl.mob","dnl.tick","dnl.slime_core"],
    PersistenceRequired:1b,
    Health:386b,
    Attributes:[{Name:generic.knockback_resistance,Base:1},{Name:generic.movement_speed,Base:0}],
    Size:9
}

add_death_event("slime_core")

@listener(on_entity_mob_tick)
def entity_mob_tick():
    if entity @s[tag=dnl.slime_core] run function ./slime_core/main

@listener(on_marker_core)
def marker_core():
    if entity @s[tag=dnl.slime_core] run function ./slime_core/core/main:
        if entity @s[tag=dnl.reset] run function ./slime_core/core/reset:
            tag @s remove dnl.reset
            kill @e[type=marker,tag=dnl.skill,tag=dnl.slime_core,distance=..30]
            as @e[type=slime,distance=..40] run tp @s ~ ~-1000 ~
            setblock ~-3 ~-22 ~-3 minecraft:structure_block{mode:"LOAD",posX:0,posY:1,posZ:0,name:"dnl:sinkhole/boss_layers"}
            setblock ~-3 ~-23 ~-3 minecraft:redstone_block
            fill ~-3 ~-23 ~-3 ~-3 ~-22 ~-3 minecraft:air
            function ../spawner/slime_core
    
@listener(on_marker_skill)
def marker_skill():
    if entity @s[tag=dnl.slime_core.rumbling] run function ./slime_core/rumbling/main


function ./slime_core/main:
    ### Init ###
    if entity @s[tag=!dnl.slime_core.init] run function ./slime_core/init:
        tag @s add dnl.slime_core.init
        effect give @s minecraft:absorption 1000000 44 true
        scoreboard players set @s dnl.phase 1
        
        scoreboard players operation #dnl.slime_core.id dnl.eid = @s dnl.eid
        as @e[type=marker,tag=dnl.core,tag=dnl.slime_core,distance=..30] run function ./slime_core/core/copy:
            scoreboard players operation @s dnl.eid = #dnl.slime_core.id dnl.eid
        unless entity @e[type=marker,tag=dnl.core,tag=dnl.slime_core,distance=..30] run function ./slime_core/core/setup:
            summon marker ~ ~ ~ {Tags:["dnl.marker","dnl.core","dnl.slime_core","dnl.new_marker"]}
            scoreboard players operation @e[type=marker,tag=dnl.new_marker] dnl.eid = @s dnl.eid
            tag @e[type=marker,tag=dnl.new_marker] remove dnl.new_marker
    
    ### Boss Bar ###
    set_bossbar slime_core purple notched_10 200 sphere 30 0 0
    store result score #dnl.slime_core.absorption dnl.int run data get entity @s AbsorptionAmount 1
    store result score @s dnl.health run data get entity @s Health 1
    scoreboard players operation @s dnl.health += #dnl.slime_core.absorption dnl.int

    ### Arena ###
    scoreboard players reset #dnl.slime_core.reset dnl.boolean
    scoreboard players operation #dnl.slime_core.id dnl.eid = @s dnl.eid
    if score #dnl.1s dnl.tick matches 1 run function ./slime_core/core/tick:
        tag @s add dnl.this
        as @e[type=marker,tag=dnl.core,tag=dnl.slime_core] if score @s dnl.eid = #dnl.slime_core.id dnl.eid at @s run function ./slime_core/core/check:
            tag @e[type=slime,tag=dnl.slime_core,tag=dnl.this] remove dnl.slime_core.in_arena
            positioned ~-12 ~-35 ~-12 run tag @e[type=slime,tag=dnl.slime_core,tag=dnl.this,dx=25,dy=45,dz=25] add dnl.slime_core.in_arena
            positioned ~-12 ~-35 ~-12 unless entity @a[dx=25,dy=45,dz=25] run function ./slime_core/core/trigger_reset
            tp @e[type=slime,tag=dnl.slime_core,tag=dnl.this,tag=!dnl.slime_core.in_arena] ~ ~ ~
        tag @s remove dnl.this
    if score #dnl.slime_core.reset dnl.boolean matches 1 run function ./slime_core/reset:
        data modify entity @s DeathLootTable set value "minecraft:empty"
        data modify entity @s Size set value 0
        remove_bossbar this
        particle poof ~ ~ ~ 0.3 1 0.3 0.0001 5 normal
        tp @s ~ ~-100 ~
        kill @s

    ### Size Change ###
    if score @s dnl.health matches ..180 if score @s dnl.phase matches 1 run function ./slime_core/phase_1/init:
        scoreboard players add @s dnl.phase 1
        data modify entity @s Size set value 8
        scoreboard players operation #dnl.slime_core.id dnl.eid = @s dnl.eid
        as @e[type=marker,tag=dnl.core,tag=dnl.slime_core] if score @s dnl.eid = #dnl.slime_core.id dnl.eid at @s run fill ~3 ~-9 ~3 ~-3 ~ ~-3 minecraft:air destroy
        kill @e[type=item,nbt={Age:0s}]
    if score @s dnl.health matches ..160 if score @s dnl.phase matches 2 run function ./slime_core/phase_2/init:
        scoreboard players add @s dnl.phase 1
        data modify entity @s Size set value 7
    if score @s dnl.health matches ..140 if score @s dnl.phase matches 3 run function ./slime_core/phase_3/init:
        scoreboard players add @s dnl.phase 1
        data modify entity @s Size set value 6
        scoreboard players operation #dnl.slime_core.id dnl.eid = @s dnl.eid
        as @e[type=marker,tag=dnl.core,tag=dnl.slime_core] if score @s dnl.eid = #dnl.slime_core.id dnl.eid at @s run fill ~3 ~-18 ~3 ~-3 ~-9 ~-3 minecraft:air destroy
        kill @e[type=item,nbt={Age:0s}]
    if score @s dnl.health matches ..120 if score @s dnl.phase matches 4 run function ./slime_core/phase_4/init:
        scoreboard players add @s dnl.phase 1
        data modify entity @s Size set value 5
    if score @s dnl.health matches ..100 if score @s dnl.phase matches 5 run function ./slime_core/phase_5/init:
        scoreboard players add @s dnl.phase 1
        data modify entity @s Size set value 4
        scoreboard players operation #dnl.slime_core.id dnl.eid = @s dnl.eid
        as @e[type=marker,tag=dnl.core,tag=dnl.slime_core] if score @s dnl.eid = #dnl.slime_core.id dnl.eid at @s run fill ~3 ~-27 ~3 ~-3 ~-18 ~-3 minecraft:air destroy
        kill @e[type=item,nbt={Age:0s}]
    if score @s dnl.health matches ..80 if score @s dnl.phase matches 6 run function ./slime_core/phase_6/init:
        scoreboard players add @s dnl.phase 1
        data modify entity @s Size set value 3
    if score @s dnl.health matches ..60 if score @s dnl.phase matches 7 run function ./slime_core/phase_7/init:
        scoreboard players add @s dnl.phase 1
        data modify entity @s Size set value 2
    if score @s dnl.health matches ..40 if score @s dnl.phase matches 8 run function ./slime_core/phase_8/init:
        scoreboard players add @s dnl.phase 1
        data modify entity @s Size set value 1
    if score @s dnl.health matches ..20 if score @s dnl.phase matches 9 run function ./slime_core/phase_9/init:
        scoreboard players add @s dnl.phase 1
        data modify entity @s Size set value 0

    ### Main ###
    scoreboard players remove @s dnl.timer 1

    if score @s dnl.timer matches ..-1 if score @s dnl.phase matches 0..9 run function ./slime_core/phase_1/select_move:
        scoreboard players set @s dnl.timer 240
        tag @s add dnl.slime_core.rumbling

    if entity @s[tag=dnl.slime_core.rumbling] run function ./slime_core/rumbling/trigger

function ./slime_core/rumbling/trigger:
    if score @s dnl.timer matches 200 at @a[distance=..30] run summon minecraft:marker ~ ~ ~ {Tags:["dnl.marker","dnl.skill","dnl.slime_core","dnl.slime_core.rumbling"]}
    if score @s dnl.phase matches 2.. if score @s dnl.timer matches 210 at @a[distance=..30] run summon minecraft:marker ~ ~ ~ {Tags:["dnl.marker","dnl.skill","dnl.slime_core","dnl.slime_core.rumbling"]}
    if score @s dnl.phase matches 4.. if score @s dnl.timer matches 220 at @a[distance=..30] run summon minecraft:marker ~ ~ ~ {Tags:["dnl.marker","dnl.skill","dnl.slime_core","dnl.slime_core.rumbling"]}
    if score @s dnl.phase matches 6.. if score @s dnl.timer matches 230 at @a[distance=..30] run summon minecraft:marker ~ ~ ~ {Tags:["dnl.marker","dnl.skill","dnl.slime_core","dnl.slime_core.rumbling"]}
    if score @s dnl.phase matches 8.. if score @s dnl.timer matches 240 at @a[distance=..30] run summon minecraft:marker ~ ~ ~ {Tags:["dnl.marker","dnl.skill","dnl.slime_core","dnl.slime_core.rumbling"]}

function ./slime_core/rumbling/main:
    scoreboard players add @s dnl.timer 1
    if score @s dnl.timer matches 1..10 if block ~ ~1 ~ minecraft:air run tp @s ~ ~1 ~
    if score @s dnl.timer matches 10..50 run particle minecraft:item_slime ~ ~ ~ 0 0 0 1 10 normal
    if score @s dnl.timer matches 50 run function dnl:summon/random_sized_slime:
        rng 1 8
        outcome 1..4:
            summon slime ~ ~ ~ {Size:0,Tags:["dnl.mob","dnl.slime_core_minion"],ActiveEffects:[{Id:11b,Amplifier:4b,Duration:40}]}
        outcome 5..7:
            summon slime ~ ~ ~ {Size:1,Tags:["dnl.mob","dnl.slime_core_minion"],ActiveEffects:[{Id:11b,Amplifier:4b,Duration:40}]}
        outcome 8:
            summon slime ~ ~ ~ {Size:4,Tags:["dnl.mob","dnl.slime_core_minion"],ActiveEffects:[{Id:11b,Amplifier:4b,Duration:40}]}
        kill @s

function ./slime_core/core/trigger_reset:
    tag @s add dnl.reset
    scoreboard players set #dnl.slime_core.reset dnl.boolean 1

function ./slime_core/death:
    remove_bossbar item
    advancement grant @a[distance=..32] only dnl:story/structures/sinkhole/slime_core
    summon marker ~ ~ ~ {Tags:["dnl.marker","dnl.slime_core.kill_noai_slime"]}
    schedule function ./slime_core/kill_noai_slime 1s
    as @e[type=marker,tag=dnl.core,tag=dnl.slime_core] if score @s dnl.eid = #dnl.death_entity_id dnl.eid at @s run function ./slime_core/summon_reward
    as @e[type=marker,tag=dnl.core,tag=dnl.slime_core] if score @s dnl.eid = #dnl.death_entity_id dnl.eid run kill @s

function ./slime_core/summon_reward:
    setblock ~ ~-33 ~ minecraft:chiseled_stone_bricks
    setblock ~ ~-32 ~ chest{LootTable:"dnl:structures/sinkhole/mobs/slimecore/reward"}

function ./slime_core/kill_noai_slime:
    as @e[type=marker,tag=dnl.slime_core.kill_noai_slime] at @s as @e[type=slime,distance=..40] run tp @s ~ ~-1000 ~
    kill @e[type=marker,tag=dnl.skill,tag=dnl.slime_core,distance=..30]
    kill @e[type=marker,tag=dnl.slime_core.kill_noai_slime]