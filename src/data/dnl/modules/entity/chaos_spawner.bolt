from ../api import
    on_technical_mob_tick
    on_marker_core
    on_marker_skill
    on_entity_mob_tick
    add_summon
    set_bossbar
    remove_bossbar
    add_death_event
    assign_lid
    rng
    outcome

from reapermc:api import
    listener

add_summon chaos_spawner marker boss 8 7 0 flame {
    Tags:["dnl.boss","dnl.marker","dnl.mob","dnl.core","dnl.tick","dnl.chaos_spawner"]
}

add_death_event("chaos_spawner")

@listener(on_technical_mob_tick)
def entity_mob_tick():
    if entity @s[tag=dnl.chaos_spawner,tag=dnl.start] run function ./chaos_spawner/main

@listener(on_marker_core)
def marker_core():
    if entity @s[tag=dnl.chaos_spawner] run function ./chaos_spawner/core/main:
        ### Init ###
        if entity @s[tag=!dnl.chaos_spawner.core.init] run function ./chaos_spawner/core/init:
            tag @s add dnl.chaos_spawner.core.init
            scoreboard players set @s dnl.health 100
            function ./chaos_spawner/set_spawner

        ### Start ###
        if entity @s[tag=!dnl.start] unless block ~ ~ ~ minecraft:spawner run function ./chaos_spawner/core/start:
            function ./chaos_spawner/damaged
            tag @s add dnl.start

        ### Reset ###
        if entity @s[tag=dnl.start] if score #dnl.1s dnl.tick matches 1 unless entity @a[distance=..30] run function ./chaos_spawner/core/reset:
            scoreboard players operation #dnl.chaos_spawner.core.lid dnl.lid = @s dnl.lid
            as @e[type=#dnl:mobs,tag=dnl.chaos_spawner.minion] if score @s dnl.lid = #dnl.chaos_spawner.core.lid dnl.lid run tp ~ -1000 ~
            as @e[type=area_effect_cloud,tag=dnl.chaos_spawner.mining_fatigue_spell] if score @s dnl.lid = #dnl.chaos_spawner.core.lid dnl.lid run kill @s
            as @e[type=marker,tag=dnl.chaos_spawner.beam] if score @s dnl.lid = #dnl.chaos_spawner.core.lid dnl.lid run kill @s
            function ../spawner/chaos_spawner
            remove_bossbar this
            kill @s

function ./chaos_spawner/set_spawner:
    setblock ~ ~ ~ spawner{SpawnCount:0,SpawnRange:0,Delay:0,MinSpawnDelay:0,MaxSpawnDelay:0,MaxNearbyEntities:0,RequiredPlayerRange:0,SpawnData:{entity:{id:"minecraft:marker"}}}

function ./chaos_spawner/main:
    ### Init ###
    if entity @s[tag=!dnl.chaos_spawner.init] run function ./chaos_spawner/init:
        tag @s add dnl.chaos_spawner.init
        scoreboard players set @s dnl.phase 1
        scoreboard players set @s dnl.timer 220

        assign_lid
        scoreboard players operation #dnl.chaos_spawner.lid dnl.lid = @s dnl.lid

        function ./chaos_spawner/set_spawner

    ### Bossbar ###
    set_bossbar chaos_spawner purple notched_10 100 sphere 30 0 0

    ### Hurt ###
    unless block ~ ~ ~ minecraft:spawner run function ./chaos_spawner/hurt:
        scoreboard players remove @s dnl.health 3
        if score @s dnl.health matches ..0 run function ./chaos_spawner/death
        if score @s dnl.health matches 1.. run function ./chaos_spawner/damaged

    ### Particle ###
    if score #dnl.5t dnl.tick matches 1 positioned ~ ~ ~ run function ./chaos_spawner/particle

    ### Phase 1 ###
    if score @s dnl.phase matches 1 run function ./chaos_spawner/phase_1/main:
        scoreboard players add @s dnl.timer 1
        if score @s dnl.timer matches 280.. run function ./chaos_spawner/phase_1/progress:
            scoreboard players add @s dnl.move 1
            scoreboard players reset @s dnl.timer
        
        if score @s dnl.move matches 1 run function ./chaos_spawner/phase_1/move_1:
            function ./chaos_spawner/mob_spawn
        
        if score @s dnl.move matches 2 run function ./chaos_spawner/phase_1/move_2:
            function ./chaos_spawner/mining_fatigue_spell
        
        if score @s dnl.move matches 3 run function ./chaos_spawner/phase_1/move_3:
            function ./chaos_spawner/beam_single

        if score @s dnl.move matches 4.. run function ./chaos_spawner/phase_1/reset:
            scoreboard players set @s dnl.move 1
            scoreboard players add @s dnl.phase 1
    
    ### Phase 2 ###
    if score @s dnl.phase matches 2 run function ./chaos_spawner/phase_2/main:
        scoreboard players add @s dnl.timer 1
        if score @s dnl.timer matches 280.. run function ./chaos_spawner/phase_2/progress:
            scoreboard players add @s dnl.move 1
            scoreboard players reset @s dnl.timer
        
        if score @s dnl.move matches 1 run function ./chaos_spawner/phase_2/move_1:
            function ./chaos_spawner/mob_spawn

        if score @s dnl.move matches 2 run function ./chaos_spawner/phase_2/move_2:
            if score @s dnl.timer matches 1 run function ./chaos_spawner/phase_2/move_2/choose_option:
                rng 1 2
                outcome 1:
                    tag @s add dnl.using_mining_fatigue_spell
                outcome 2:
                    tag @s add dnl.using_beam_single
            if entity @s[tag=dnl.using_mining_fatigue_spell] run function ./chaos_spawner/mining_fatigue_spell
            if entity @s[tag=dnl.using_beam_single] run function ./chaos_spawner/beam_single
        
        if score @s dnl.move matches 3 run function ./chaos_spawner/phase_2/move_3:
            if entity @s[tag=dnl.using_mining_fatigue_spell] run function ./chaos_spawner/beam_single
            if entity @s[tag=dnl.using_beam_single] run function ./chaos_spawner/mining_fatigue_spell
        
        if score @s dnl.move matches 4 run function ./chaos_spawner/phase_2/reset:
            scoreboard players set @s dnl.move 1
            tag @s remove dnl.using_mining_fatigue_spell
            tag @s remove dnl.using_beam_single

    ### Phase 3+ ###
    if score @s dnl.health matches ..75 run function ./chaos_spawner/phase_3/main:
        if score @s dnl.phase matches ..2 run function ./chaos_spawner/phase_3/init:
            scoreboard players set @s dnl.phase 3
            scoreboard players set @s dnl.timer 220
            scoreboard players reset @s dnl.move
            tag @s remove dnl.using_mining_fatigue_spell
            tag @s remove dnl.using_beam_single
            scoreboard players operation #dnl.chaos_spawner.lid dnl.lid = @s dnl.lid
            as @e[type=area_effect_cloud,tag=dnl.chaos_spawner.mining_fatigue_spell] if score @s dnl.lid = #dnl.chaos_spawner.lid dnl.lid run kill @s
        
        scoreboard players add @s dnl.timer 1
        if score @s dnl.timer matches 280.. if score @s dnl.phase matches 3 run function ./chaos_spawner/phase_3/progress:
            scoreboard players add @s dnl.move 1
            scoreboard players reset @s dnl.timer
        
        if score @s dnl.timer matches 280.. if score @s dnl.phase matches 4 run function ./chaos_spawner/phase_4/progress:
            scoreboard players reset @s dnl.timer
            rng 1 3
            outcome 1:
                scoreboard players add @s dnl.move 1
            outcome 2:
                scoreboard players add @s dnl.move 2
            outcome 3:
                scoreboard players add @s dnl.move 3
            if score @s dnl.move matches 10.. run scoreboard players remove @s dnl.move 9
        
        if score @s dnl.move matches 1 run function ./chaos_spawner/phase_3/move_1:
            function ./chaos_spawner/mob_spawn
            function ./chaos_spawner/spawn_baby_zombie
        
        if score @s dnl.move matches 2 run function ./chaos_spawner/phase_3/move_2:
            function ./chaos_spawner/mining_fatigue_spell
            positioned ~ ~1 ~ run function ./chaos_spawner/mining_fatigue_spell
            positioned ~ ~2 ~ run function ./chaos_spawner/mining_fatigue_spell
        
        if score @s dnl.move matches 3 run function ./chaos_spawner/phase_3/move_3:
            function ./chaos_spawner/beam_eight
        
        if score @s dnl.move matches 4 run function ./chaos_spawner/phase_3/move_4:
            function ./chaos_spawner/spawn_baby_zombie
            function ./chaos_spawner/beam_single_twice

        if score @s dnl.move matches 5 run function ./chaos_spawner/phase_3/move_5:
            function ./chaos_spawner/mining_fatigue_spell_fast
            positioned ~ ~1 ~ run function ./chaos_spawner/mining_fatigue_spell_fast
            positioned ~ ~2 ~ run function ./chaos_spawner/mining_fatigue_spell_fast
        
        if score @s dnl.move matches 6 run function ./chaos_spawner/phase_3/move_6:
            function ./chaos_spawner/beam_eight_then_single

        if score @s dnl.move matches 7 run function ./chaos_spawner/phase_3/move_7:
            function ./chaos_spawner/mob_spawn
            function ./chaos_spawner/spawn_baby_zombie
            function ./chaos_spawner/beam_eight

        if score @s dnl.move matches 8 run function ./chaos_spawner/phase_3/move_8:
            function ./chaos_spawner/mob_spawn
            function ./chaos_spawner/beam_eight_then_single
        
        if score @s dnl.move matches 9 run function ./chaos_spawner/phase_3/move_9:
            function ./chaos_spawner/mining_fatigue_spell_fast
            positioned ~ ~1 ~ run function ./chaos_spawner/mining_fatigue_spell_fast
            positioned ~ ~2 ~ run function ./chaos_spawner/mining_fatigue_spell_fast
        
        if score @s dnl.move matches 10 run function ./chaos_spawner/phase_3/reset:
            scoreboard players add @s dnl.phase 1
            scoreboard players set @s dnl.move 1

### General ###

function ./chaos_spawner/particle:
    particle minecraft:flame ~ ~0.5 ~ 0.5 0.5 0.5 0.001 2 normal
    particle minecraft:enchant ~ ~0.5 ~ 0.5 0.5 0.5 0.001 5 normal

function ./chaos_spawner/damaged:
    playsound minecraft:entity.ender_dragon.growl master @a ~ ~ ~ 0.5 2 0
    particle witch ~ ~ ~ 0.3 0.3 0.3 1.0 20 normal
    function ./chaos_spawner/set_spawner

function ./chaos_spawner/death:
    playsound minecraft:entity.ender_dragon.growl master @a ~ ~ ~ 1 0.1 0
    particle witch ~ ~ ~ 0.4 0.4 0.4 1.0 25 normal
    particle flame ~ ~ ~ 0.4 0.4 0.4 1.0 25 normal
    particle poof ~ ~ ~ 0.4 0.4 0.4 1.0 25 normal
    scoreboard players operation #dnl.chaos_spawner.core.lid dnl.lid = @s dnl.lid
    as @e[type=#dnl:mobs,tag=dnl.chaos_spawner.minion] if score @s dnl.lid = #dnl.chaos_spawner.core.lid dnl.lid run tp ~ -1000 ~
    as @e[type=area_effect_cloud,tag=dnl.chaos_spawner.mining_fatigue_spell] if score @s dnl.lid = #dnl.chaos_spawner.core.lid dnl.lid run kill @s
    as @e[type=marker,tag=dnl.chaos_spawner.beam] if score @s dnl.lid = #dnl.chaos_spawner.core.lid dnl.lid run kill @s
    remove_bossbar this
    kill @s

### Mob Spawn ###
function ./chaos_spawner/mob_spawn:
    if score @s dnl.timer matches 1 run function ./chaos_spawner/mob_spawn/summon_all:
        positioned ~2 ~-1 ~ run function ./chaos_spawner/mob_spawn/summon_mobs
        positioned ~ ~-1 ~2 run function ./chaos_spawner/mob_spawn/summon_mobs
        positioned ~-2 ~-1 ~ run function ./chaos_spawner/mob_spawn/summon_mobs
        positioned ~ ~-1 ~-2 run function ./chaos_spawner/mob_spawn/summon_mobs
        scoreboard players operation #dnl.chaos_spawner.lid dnl.lid = @s dnl.lid
        scoreboard players operation @e[tag=dnl.chaos_spawner.minion,tag=dnl.new] dnl.lid = #dnl.chaos_spawner.lid dnl.lid
        tag @e[tag=dnl.chaos_spawner.minion] remove dnl.new

function ./chaos_spawner/spawn_baby_zombie:
    if score @s dnl.timer matches 1 positioned ~ ~2 ~ run function ./chaos_spawner/spawn_baby_zombie/summon:
        summon zombie ~ ~ ~ {
            DeathLootTable:"minecraft:empty",
            Health:100f,
            IsBaby:1b,
            Tags:["dnl.mob","dnl.chaos_spawner.minion","dnl.chaos_spawner.baby_zombie","dnl.new"],
            HandItems:[{id:"minecraft:diamond_axe",Count:1b},{}],
            HandDropChances:[0.000F,0.000F],
            ArmorItems:[{id:"minecraft:diamond_boots",Count:1b},{id:"minecraft:diamond_leggings",Count:1b},{id:"minecraft:diamond_chestplate",Count:1b},{id:"minecraft:diamond_helmet",Count:1b}],
            ArmorDropChances:[0.000F,0.000F,0.000F,0.000F],
            Attributes:[{Name:generic.max_health,Base:100},{Name:generic.knockback_resistance,Base:1},{Name:generic.attack_damage,Base:5}]}
        particle poof ~ ~ ~ 0.3 1 0.3 0.0001 5 normal
        scoreboard players operation #dnl.chaos_spawner.lid dnl.lid = @s dnl.lid
        scoreboard players operation @e[tag=dnl.chaos_spawner.baby_zombie,tag=dnl.new] dnl.lid = #dnl.chaos_spawner.lid dnl.lid
        tag @e[tag=dnl.chaos_spawner.baby_zombie] remove dnl.new
    if score @s dnl.timer matches 279 run scoreboard players operation #dnl.chaos_spawner.lid dnl.lid = @s dnl.lid
    if score @s dnl.timer matches 279 as @e[type=zombie,tag=dnl.chaos_spawner.baby_zombie] if score @s dnl.lid = #dnl.chaos_spawner.lid dnl.lid run function ./chaos_spawner/spawn_baby_zombie/kill:
        particle poof ~ ~0.5 ~ 0.3 0.3 0.3 0.0001 10 normal
        tp @s ~ ~-1000 ~
        kill @s

function ./chaos_spawner/mob_spawn/summon_mobs:
    particle poof ~ ~ ~ 0.3 1 0.3 0.0001 5 normal
    rng 1 6
    outcome 1..3:
        summon minecraft:zombie ~ ~ ~ {
            DeathLootTable:"minecraft:empty",
            Tags:["dnl.mob","dnl.chaos_spawner.minion","dnl.new"]
        }
    outcome 4:
        summon minecraft:skeleton ~ ~ ~ {
            DeathLootTable:"minecraft:empty",
            Tags:["dnl.mob","dnl.chaos_spawner.minion","dnl.new"]
        }
    outcome 5..6:
        summon minecraft:spider ~ ~ ~ {
            DeathLootTable:"minecraft:empty",
            Tags:["dnl.mob","dnl.chaos_spawner.minion","dnl.new"]
        }

### Mining Fatigue Spell ###
function ./chaos_spawner/mining_fatigue_spell:
    if score @s dnl.timer matches 1 run function ./chaos_spawner/mining_fatigue_spell/summon:
        summon area_effect_cloud ~ ~-1 ~ {
            Tags:["dnl.area_effect_cloud","dnl.chaos_spawner.mining_fatigue_spell","dnl.new"],
            Particle:"witch",
            ReapplicationDelay:20,
            Radius:3f,
            RadiusPerTick:0.05f,
            RadiusOnUse:-0.5f,
            Duration:200,
            WaitTime:10,
            Effects:[{Id:4b,Amplifier:2b,Duration:100,ShowParticles:1b},{Id:20b,Amplifier:4b,Duration:100,ShowParticles:1b}]}
        scoreboard players operation #dnl.chaos_spawner.lid dnl.lid = @s dnl.lid
        scoreboard players operation @e[type=area_effect_cloud,tag=dnl.chaos_spawner.mining_fatigue_spell,tag=dnl.new] dnl.lid = #dnl.chaos_spawner.lid dnl.lid
        tag @e[type=area_effect_cloud,tag=dnl.chaos_spawner.mining_fatigue_spell] remove dnl.new

function ./chaos_spawner/mining_fatigue_spell_fast:
    if score @s dnl.timer matches 1 run function ./chaos_spawner/mining_fatigue_spell_fast/summon:
        summon area_effect_cloud ~ ~-1 ~ {
            Tags:["dnl.area_effect_cloud","dnl.chaos_spawner.mining_fatigue_spell","dnl.new"],
            Particle:"witch",
            ReapplicationDelay:20,
            Radius:3f,
            RadiusPerTick:0.075f,
            RadiusOnUse:-0.5f,
            Duration:150,
            WaitTime:10,
            Effects:[{Id:4b,Amplifier:2b,Duration:100,ShowParticles:1b},{Id:20b,Amplifier:4b,Duration:100,ShowParticles:1b}]}
        scoreboard players operation #dnl.chaos_spawner.lid dnl.lid = @s dnl.lid
        scoreboard players operation @e[type=area_effect_cloud,tag=dnl.chaos_spawner.mining_fatigue_spell,tag=dnl.new] dnl.lid = #dnl.chaos_spawner.lid dnl.lid
        tag @e[type=area_effect_cloud,tag=dnl.chaos_spawner.mining_fatigue_spell] remove dnl.new

### Beam ###
@listener(on_marker_skill)
def marker_skill():
    if entity @s[tag=dnl.chaos_spawner.beam] run function ./chaos_spawner/beam/type:
        if entity @s[tag=dnl.chaos_spawner.beam_single] run function ./chaos_spawner/beam_single/main
        if entity @s[tag=dnl.chaos_spawner.beam_single_strong] run function ./chaos_spawner/beam_single_strong/main
        if entity @s[tag=dnl.chaos_spawner.beam_eight] run function ./chaos_spawner/beam_eight/main

function ./chaos_spawner/beam/prepare:
    for i in range(40):
        j=i*0.3
        positioned ^ ^ ^j run particle dust_color_transition 1.000 0.051 0.051 1 0.000 0.000 0.000 ~ ~ ~ 0.1 0.1 0.1 0.01 5 force

function ./chaos_spawner/beam/shoot:
    playsound minecraft:entity.blaze.shoot master @a[distance=..16] ~ ~ ~ 1 1.0
    for i in range(40):
        j=i*0.3
        positioned ^ ^ ^j run function ./chaos_spawner/beam/damage

function ./chaos_spawner/beam/damage:
    particle flame ~ ~ ~ 0 0 0 0.01 1 force
    particle poof ~ ~ ~ 0 0 0 0.01 1 force
    effect give @a[dx=0] minecraft:instant_damage 1 0 true

function ./chaos_spawner/beam/prepare_strong:
    for i in range(40):
        j=i*0.3
        positioned ^ ^ ^j run particle dust_color_transition 0.439 0.024 0.024 1 0.000 0.000 0.000 ~ ~ ~ 0.1 0.1 0.1 0.01 5 force

function ./chaos_spawner/beam/shoot_strong:
    playsound minecraft:entity.blaze.shoot master @a[distance=..16] ~ ~ ~ 1 0.1
    for i in range(40):
        j=i*0.3
        positioned ^ ^ ^j run function ./chaos_spawner/beam/damage_strong

function ./chaos_spawner/beam/damage_strong:
    particle flame ~ ~ ~ 0 0 0 0.01 1 force
    particle poof ~ ~ ~ 0 0 0 0.01 1 force
    effect give @a[dx=0] minecraft:instant_damage 1 1 true

### Beam Single ###
function ./chaos_spawner/beam_single:
    if score @s dnl.timer matches 1 run function ./chaos_spawner/beam_single/summon

function ./chaos_spawner/beam_single/summon:
    summon marker ~ ~0.5 ~ {Tags:["dnl.marker","dnl.skill","dnl.chaos_spawner.beam","dnl.chaos_spawner.beam_single","dnl.new"]}
    scoreboard players operation #dnl.chaos_spawner.lid dnl.lid = @s dnl.lid
    scoreboard players operation @e[type=marker,tag=dnl.chaos_spawner.beam,tag=dnl.new] dnl.lid = #dnl.chaos_spawner.lid dnl.lid
    tag @e[type=marker,tag=dnl.chaos_spawner.beam] remove dnl.new

function ./chaos_spawner/beam_single/main:
    scoreboard players add @s dnl.timer 1
    if score @s dnl.timer matches 9 run tp @s ~ ~ ~ facing entity @p[distance=..15] eyes
    if score @s dnl.timer matches 10 run function ./chaos_spawner/beam/prepare
    if score @s dnl.timer matches 25 run function ./chaos_spawner/beam/shoot
    if score @s dnl.timer matches 44 run tp @s ~ ~ ~ facing entity @p[distance=..15] eyes
    if score @s dnl.timer matches 45 run function ./chaos_spawner/beam/prepare
    if score @s dnl.timer matches 60 run function ./chaos_spawner/beam/shoot
    if score @s dnl.timer matches 79 run tp @s ~ ~ ~ facing entity @p[distance=..15] eyes
    if score @s dnl.timer matches 80 run function ./chaos_spawner/beam/prepare
    if score @s dnl.timer matches 95 run function ./chaos_spawner/beam/shoot
    if score @s dnl.timer matches 95 run kill @s

### Beam Single Twice ###
function ./chaos_spawner/beam_single_twice:
    if score @s dnl.timer matches 1 run function ./chaos_spawner/beam_single/summon
    if score @s dnl.timer matches 135 run function ./chaos_spawner/beam_single/summon

### Beam Eight Then Single ###
function ./chaos_spawner/beam_eight_then_single:
    if score @s dnl.timer matches 1 run function ./chaos_spawner/beam_eight/summon
    if score @s dnl.timer matches 135 run function ./chaos_spawner/beam_single_strong/summon

function ./chaos_spawner/beam_single_strong/summon:
    summon marker ~ ~0.5 ~ {Tags:["dnl.marker","dnl.skill","dnl.chaos_spawner.beam","dnl.chaos_spawner.beam_single_strong","dnl.new"]}
    scoreboard players operation #dnl.chaos_spawner.lid dnl.lid = @s dnl.lid
    scoreboard players operation @e[type=marker,tag=dnl.chaos_spawner.beam,tag=dnl.new] dnl.lid = #dnl.chaos_spawner.lid dnl.lid
    tag @e[type=marker,tag=dnl.chaos_spawner.beam] remove dnl.new

function ./chaos_spawner/beam_single_strong/main:
    scoreboard players add @s dnl.timer 1
    if score @s dnl.timer matches 9 run tp @s ~ ~ ~ facing entity @p[distance=..15] eyes
    if score @s dnl.timer matches 10 run function ./chaos_spawner/beam/prepare_strong
    if score @s dnl.timer matches 25 run function ./chaos_spawner/beam/shoot_strong
    if score @s dnl.timer matches 44 run tp @s ~ ~ ~ facing entity @p[distance=..15] eyes
    if score @s dnl.timer matches 45 run function ./chaos_spawner/beam/prepare_strong
    if score @s dnl.timer matches 60 run function ./chaos_spawner/beam/shoot_strong
    if score @s dnl.timer matches 79 run tp @s ~ ~ ~ facing entity @p[distance=..15] eyes
    if score @s dnl.timer matches 80 run function ./chaos_spawner/beam/prepare_strong
    if score @s dnl.timer matches 95 run function ./chaos_spawner/beam/shoot_strong
    if score @s dnl.timer matches 95 run kill @s

### Beam Eight ###
function ./chaos_spawner/beam_eight:
    if score @s dnl.timer matches 1 run function ./chaos_spawner/beam_eight/summon

function ./chaos_spawner/beam_eight/summon:
    summon marker ~ ~0.5 ~ {Tags:["dnl.marker","dnl.skill","dnl.chaos_spawner.beam","dnl.chaos_spawner.beam_eight","dnl.new"],Rotation:[0.0f,0.0f]}
    summon marker ~ ~0.5 ~ {Tags:["dnl.marker","dnl.skill","dnl.chaos_spawner.beam","dnl.chaos_spawner.beam_eight","dnl.new"],Rotation:[45.0f,0.0f]}
    summon marker ~ ~0.5 ~ {Tags:["dnl.marker","dnl.skill","dnl.chaos_spawner.beam","dnl.chaos_spawner.beam_eight","dnl.new"],Rotation:[90.0f,0.0f]}
    summon marker ~ ~0.5 ~ {Tags:["dnl.marker","dnl.skill","dnl.chaos_spawner.beam","dnl.chaos_spawner.beam_eight","dnl.new"],Rotation:[135.0f,0.0f]}
    summon marker ~ ~0.5 ~ {Tags:["dnl.marker","dnl.skill","dnl.chaos_spawner.beam","dnl.chaos_spawner.beam_eight","dnl.new"],Rotation:[180.0f,0.0f]}
    summon marker ~ ~0.5 ~ {Tags:["dnl.marker","dnl.skill","dnl.chaos_spawner.beam","dnl.chaos_spawner.beam_eight","dnl.new"],Rotation:[225.0f,0.0f]}
    summon marker ~ ~0.5 ~ {Tags:["dnl.marker","dnl.skill","dnl.chaos_spawner.beam","dnl.chaos_spawner.beam_eight","dnl.new"],Rotation:[270.0f,0.0f]}
    summon marker ~ ~0.5 ~ {Tags:["dnl.marker","dnl.skill","dnl.chaos_spawner.beam","dnl.chaos_spawner.beam_eight","dnl.new"],Rotation:[315.0f,0.0f]}
    scoreboard players operation #dnl.chaos_spawner.lid dnl.lid = @s dnl.lid
    scoreboard players operation @e[type=marker,tag=dnl.chaos_spawner.beam,tag=dnl.new] dnl.lid = #dnl.chaos_spawner.lid dnl.lid
    tag @e[type=marker,tag=dnl.chaos_spawner.beam] remove dnl.new


function ./chaos_spawner/beam_eight/main:
    scoreboard players add @s dnl.timer 1
    if score @s dnl.timer matches 10 run function ./chaos_spawner/beam/prepare
    if score @s dnl.timer matches 25 run function ./chaos_spawner/beam/shoot
    if score @s dnl.timer matches 44 run tp @s ~ ~ ~ ~22.5 ~
    if score @s dnl.timer matches 45 run function ./chaos_spawner/beam/prepare
    if score @s dnl.timer matches 60 run function ./chaos_spawner/beam/shoot
    if score @s dnl.timer matches 79 run tp @s ~ ~ ~ ~22.5 ~
    if score @s dnl.timer matches 80 run function ./chaos_spawner/beam/prepare
    if score @s dnl.timer matches 95 run function ./chaos_spawner/beam/shoot
    if score @s dnl.timer matches 95 run kill @s
    
