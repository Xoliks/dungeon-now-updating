from ../api import
    on_technical_mob_tick
    add_summon

from reapermc:api import
    listener

add_summon hunter_bow armor_stand none 8 7 0 flame {
    Rotation:[90.0f,0.0f],
    Tags:["dnl.mob","dnl.tick","dnl.hunter_bow"],
    Invulnerable:0b,
    NoGravity:1b,
    Invisible:1b,
    ShowArms:1b,
    Pose:{RightArm:[90f,90f,0f]}
    }

@listener(on_technical_mob_tick)
def technical_mob_tick():
    if entity @s[tag=dnl.hunter_bow] run function ./hunter_bow/main

function ./hunter_bow/main:
    ### Init ###
    if entity @s[tag=!dnl.hunter_bow.init] run function ./hunter_bow/init:
        tag @s add dnl.hunter_bow.init
        loot replace entity @s weapon.mainhand loot dnl:item/broken_bow
    
    ### Main ###
    if score #dnl.1s dnl.tick matches 1 run function ./hunter_bow/check_hand:
        unless entity @s[nbt={HandItems:[{Count:1b}]}] run tag @s add dnl.hunter_bow.start_summoning
    
    if entity @s[tag=dnl.hunter_bow.start_summoning] positioned ~ ~1 ~ run function ./hunter_bow/start_summoning:
        scoreboard players add @s dnl.timer 1
        particle minecraft:ash ~ ~2.5 ~ 0.5 0.5 0.5 16 10
        if score @s dnl.timer matches 100.. run function ./hunter_bow/summon_hunter_ghost:
            particle minecraft:flash ~ ~2.5 ~ 0.5 0.5 0.5 16 10
            playsound entity.enderman.teleport ambient @a[distance=..16] ~ ~ ~ 1.0 2 1
            function ../summon/hunter_ghost
            kill @s
