from ../api import
    on_entity_mob_tick
    on_marker_core
    on_marker_skill
    add_summon
    add_death_event
    summon_skill
    assign_lid
    rng
    outcome

from reapermc:api import
    listener

add_summon guardian_ghast ghast boss 8 7 0 flame {
    DeathLootTable:"dnl:entity/guardian_ghast/death",
    Tags:["dnl.tick","dnl.mob","dnl.guardian_ghast"],
    NoAI:0b,
    Silent:1b,
    Invulnerable:1b,
    PersistenceRequired:1b,
    ExplosionPower:3,
    Health:1f,
    Attributes:[{Name:generic.max_health,Base:1000}]
}

add_death_event("guardian_ghast")

@listener(on_entity_mob_tick)
def entity_mob_tick():
    if entity @s[tag=dnl.guardian_ghast] run function ./guardian_ghast/main

@listener(on_marker_core)
def marker_core():
    if entity @s[tag=dnl.guardian_ghast] run function ./guardian_ghast/core/main:
        ### Init ###
        if entity @s[tag=dnl.guardian_ghast.core.init] run function ./guardian_ghast/core/init:
            tag @s add dnl.guardian_ghast.core.init

        ### Start ###
        if entity @s[tag=dnl.start] if score #dnl.1s dnl.tick matches 1 run function ./guardian_ghast/core/start:
            scoreboard players operation #dnl.guardian_ghast.core.lid dnl.lid = @s dnl.lid
            as @e[type=ghast,tag=dnl.guardian_ghast,tag=!dnl.guardian_ghast.attacker] if score @s dnl.lid = #dnl.guardian_ghast.core.lid dnl.lid unless entity @s[dx=0] run tp @s ~ ~ ~
        
        ### Reset ###
        if entity @s[tag=dnl.reset] run function ./guardian_ghast/core/reset:
            scoreboard players reset @s dnl.timer
            tag @s remove dnl.reset
            tag @s remove dnl.guardian_ghast.core.summon_pillar
            tag @s remove dnl.guardian_ghast.core.stunned
            tag @s remove dnl.guardian_ghast.core.track_pillars
            tag @s remove dnl.guardian_ghast.core.pillar_1_destroyed
            tag @s remove dnl.guardian_ghast.core.pillar_2_destroyed
            tag @s remove dnl.guardian_ghast.core.pillar_3_destroyed
            tag @s remove dnl.guardian_ghast.core.pillar_4_destroyed
            scoreboard players operation #dnl.guardian_ghast.core.lid dnl.lid = @s dnl.lid
            as @e[type=ghast,tag=dnl.guardian_ghast] if score @s dnl.lid = #dnl.guardian_ghast.core.lid dnl.lid unless entity @s[dx=0] run tp @s ~ ~ ~
            positioned ~8.5 ~12 ~8.5 run fill ~-1 ~ ~-1 ~1 ~7 ~1 minecraft:air
            positioned ~-8.5 ~12 ~8.5 run fill ~-1 ~ ~-1 ~1 ~7 ~1 minecraft:air
            positioned ~-8.5 ~12 ~-8.5 run fill ~-1 ~ ~-1 ~1 ~7 ~1 minecraft:air
            positioned ~8.5 ~12 ~-8.5 run fill ~-1 ~ ~-1 ~1 ~7 ~1 minecraft:air
            kill @e[type=item,nbt={Age:0s}]

        ### Particle Effect ###
        if entity @s[tag=dnl.guardian_ghast.core.stunned] run function ./guardian_ghast/core/stunned:
            scoreboard players add @s dnl.timer 1
            if score @s dnl.timer matches 1 run tp @s ~ ~ ~ 0 0
            positioned ^ ^6 ^3 run particle minecraft:falling_obsidian_tear ~ ~ ~ 0.1 0 0.1 0.01 10 force
            positioned ^ ^6 ^3 run particle minecraft:dragon_breath ~ ~ ~ 0.1 0 0.1 0.05 10 force
            tp @s ~ ~ ~ ~10 ~
            if score @s dnl.timer matches 160.. run tag @s remove dnl.guardian_ghast.core.stunned
            if score @s dnl.timer matches 160.. run scoreboard players reset @s dnl.timer
        
        ### Summon Pillars ###
        if entity @s[tag=dnl.guardian_ghast.core.summon_pillar] run function ./guardian_ghast/core/pillar_summon:
            scoreboard players add @s dnl.timer 1
            if score @s dnl.timer matches 1 run function ./guardian_ghast/core/retreat_ghast:
                scoreboard players operation #dnl.guardian_ghast.core.lid dnl.lid = @s dnl.lid
                as @e[type=ghast,tag=dnl.guardian_ghast] if score @s dnl.lid = #dnl.guardian_ghast.core.lid dnl.lid if entity @s[tag=dnl.guardian_ghast.attacker] at @s run function ./guardian_ghast/attacker/retreat
            if score @s dnl.timer matches 40 positioned ~8.5 ~12 ~8.5 run summon_skill guardian_ghast pillar_generator
            if score @s dnl.timer matches 70 positioned ~-8.5 ~12 ~8.5 run summon_skill guardian_ghast pillar_generator
            if score @s dnl.timer matches 100 positioned ~-8.5 ~12 ~-8.5 run summon_skill guardian_ghast pillar_generator
            if score @s dnl.timer matches 130 positioned ~8.5 ~12 ~-8.5 run summon_skill guardian_ghast pillar_generator
            if score @s dnl.timer matches 190 run playsound minecraft:entity.lightning_bolt.thunder master @a ~ ~ ~ 4 2
            if score @s dnl.timer matches 210 run function ./guardian_ghast/core/pillar_summon/end:
                scoreboard players reset @s dnl.timer
                tag @s add dnl.guardian_ghast.core.track_pillars
                tag @s remove dnl.guardian_ghast.core.summon_pillar
                tag @s remove dnl.guardian_ghast.core.pillar_1_destroyed
                tag @s remove dnl.guardian_ghast.core.pillar_2_destroyed
                tag @s remove dnl.guardian_ghast.core.pillar_3_destroyed
                tag @s remove dnl.guardian_ghast.core.pillar_4_destroyed
                playsound minecraft:entity.ghast.warn master @a ~ ~ ~ 4 0.8
                scoreboard players operation #dnl.guardian_ghast.core.lid dnl.lid = @s dnl.lid
                as @e[type=ghast,tag=dnl.guardian_ghast] if score @s dnl.lid = #dnl.guardian_ghast.core.lid dnl.lid run tag @s add dnl.guardian_ghast.awoken
        
        ### Track Pillars ###
        if entity @s[tag=dnl.guardian_ghast.core.track_pillars] if score #dnl.1s dnl.tick matches 1 run function ./guardian_ghast/core/track_pillars:
            unless entity @s[tag=dnl.guardian_ghast.core.pillar_1_destroyed] unless block ~8.5 ~12 ~8.5 minecraft:bookshelf run function ./guardian_ghast/core/track_pillars/pillar_1_destroyed:
                tag @s add dnl.guardian_ghast.core.pillar_1_destroyed
                scoreboard players operation #dnl.guardian_ghast.core.sid dnl.sid = @s dnl.sid
                as @e[type=marker,tag=dnl.sisters_of_sorrow] if score @s dnl.sid = #dnl.guardian_ghast.core.sid dnl.sid run scoreboard players remove @s dnl.health 1
            unless entity @s[tag=dnl.guardian_ghast.core.pillar_2_destroyed] unless block ~-8.5 ~12 ~8.5 minecraft:bookshelf run function ./guardian_ghast/core/track_pillars/pillar_2_destroyed:
                tag @s add dnl.guardian_ghast.core.pillar_2_destroyed
                scoreboard players operation #dnl.guardian_ghast.core.sid dnl.sid = @s dnl.sid
                as @e[type=marker,tag=dnl.sisters_of_sorrow] if score @s dnl.sid = #dnl.guardian_ghast.core.sid dnl.sid run scoreboard players remove @s dnl.health 1
            unless entity @s[tag=dnl.guardian_ghast.core.pillar_3_destroyed] unless block ~-8.5 ~12 ~-8.5 minecraft:bookshelf run function ./guardian_ghast/core/track_pillars/pillar_3_destroyed:
                tag @s add dnl.guardian_ghast.core.pillar_3_destroyed
                scoreboard players operation #dnl.guardian_ghast.core.sid dnl.sid = @s dnl.sid
                as @e[type=marker,tag=dnl.sisters_of_sorrow] if score @s dnl.sid = #dnl.guardian_ghast.core.sid dnl.sid run scoreboard players remove @s dnl.health 1
            unless entity @s[tag=dnl.guardian_ghast.core.pillar_4_destroyed] unless block ~8.5 ~12 ~-8.5 minecraft:bookshelf run function ./guardian_ghast/core/track_pillars/pillar_4_destroyed:
                tag @s add dnl.guardian_ghast.core.pillar_4_destroyed
                scoreboard players operation #dnl.guardian_ghast.core.sid dnl.sid = @s dnl.sid
                as @e[type=marker,tag=dnl.sisters_of_sorrow] if score @s dnl.sid = #dnl.guardian_ghast.core.sid dnl.sid run scoreboard players remove @s dnl.health 1
            

@listener(on_marker_skill)
def marker_skill():
    if entity @s[tag=dnl.guardian_ghast.pillar_generator] run function ./guardian_ghast/pillar_generator/main

function ./guardian_ghast/pillar_generator/main:
    scoreboard players add @s dnl.timer 1
    if score @s dnl.timer matches 20 run playsound minecraft:block.enchantment_table.use master @a ~ ~ ~ 4 0.5
    if score @s dnl.timer matches 40.. run function ./guardian_ghast/pillar_generator/spawn_pillar:
        tag @s remove dnl.guardian_ghast.core.summon_pillar
        playsound minecraft:entity.dragon_fireball.explode master @a ~ ~ ~ 4 1
        particle explosion ~ ~ ~ 1 2 1 0.001 20 force
        setblock ~ ~ ~ minecraft:structure_block{mode:"LOAD",posX:-1,posY:0,posZ:-1,name:"dnl:temple_of_tears/pillar_of_relic/top"}
        setblock ~ ~1 ~ minecraft:redstone_block
        setblock ~ ~1 ~ minecraft:tnt
        setblock ~ ~ ~ minecraft:bookshelf
        kill @s

function ./guardian_ghast/main:
    ### Init ###
    if entity @s[tag=!dnl.guardian_ghast.init] run function ./guardian_ghast/init:
        tag @s add dnl.guardian_ghast.init
        tag @s add dnl.guardian_ghast.sleep
        assign_lid
        scoreboard players set @s dnl.phase 0
        scoreboard players set @s dnl.health 12

        tp ~0.5 ~ ~0.5

        ### Summon and Link ID Guardian Ghast Core Marker ###
        summon marker ~0.5 ~ ~0.5 {Tags:["dnl.marker","dnl.core","dnl.guardian_ghast","dnl.start","dnl.core.new_marker"]}
        scoreboard players operation @e[type=marker,tag=dnl.core.new_marker] dnl.lid = @s dnl.lid
        tag @e[type=marker,tag=dnl.core.new_marker] remove dnl.core.new_marker

    ### Potion Effect ###
    effect give @s minecraft:resistance 2 5 true
    
    ### State ###
    if entity @s[tag=dnl.guardian_ghast.stunned] run function ./guardian_ghast/stunned
    if entity @s[tag=dnl.guardian_ghast.stunned_long] run function ./guardian_ghast/stunned_long
    if entity @s[tag=dnl.guardian_ghast.sleep] run function ./guardian_ghast/sleep
    if entity @s[tag=dnl.guardian_ghast.awoken] run function ./guardian_ghast/awoken
    if entity @s[tag=dnl.guardian_ghast.attacker] run function ./guardian_ghast/attacker

    ### Color ###
    if entity @s[tag=dnl.guardian_ghast.red] run function ./guardian_ghast/particle/red
    if entity @s[tag=dnl.guardian_ghast.yellow] run function ./guardian_ghast/particle/yellow
    if entity @s[tag=dnl.guardian_ghast.blue] run function ./guardian_ghast/particle/blue

function ./guardian_ghast/particle/red:
    particle dust_color_transition 1.000 0.000 0.000 1 0.000 0.000 0.000 ~3 ~5 ~ 0 0 1.5 0.01 10 force
    particle dust_color_transition 1.000 0.000 0.000 1 0.000 0.000 0.000 ~-3 ~5 ~ 0 0 1.5 0.01 10 force
    particle dust_color_transition 1.000 0.000 0.000 1 0.000 0.000 0.000 ~ ~5 ~3 1.5 0 0 0.01 10 force
    particle dust_color_transition 1.000 0.000 0.000 1 0.000 0.000 0.000 ~ ~5 ~-3 1.5 0 0 0.01 10 force
    # particle minecraft:soul ~ ~ ~ 3 3 3 0.01 10 force
    # positioned ^1.5 ^2.5 ^2.5 run particle dust_color_transition 1.000 0.000 0.000 1 0.000 0.000 0.000 ~ ~ ~ 0 0 0 1 0 force
    # positioned ^-1.5 ^2.5 ^2.5 run particle dust_color_transition 1.000 0.000 0.000 1 0.000 0.000 0.000 ~ ~ ~ 0 0 0 1 0 force

function ./guardian_ghast/particle/blue:
    particle dust_color_transition 0.000 0.000 1.000 1 0.000 0.000 0.000 ~3 ~5 ~ 0 0 1.5 0.01 10 force
    particle dust_color_transition 0.000 0.000 1.000 1 0.000 0.000 0.000 ~-3 ~5 ~ 0 0 1.5 0.01 10 force
    particle dust_color_transition 0.000 0.000 1.000 1 0.000 0.000 0.000 ~ ~5 ~3 1.5 0 0 0.01 10 force
    particle dust_color_transition 0.000 0.000 1.000 1 0.000 0.000 0.000 ~ ~5 ~-3 1.5 0 0 0.01 10 force
    # positioned ^1.5 ^2.5 ^2.5 run particle dust_color_transition 0.031 0.000 0.000 1 0.000 0.000 0.000 ~ ~ ~ 0 0 0 1 0 force
    # positioned ^-1.5 ^2.5 ^2.5 run particle dust_color_transition 0.031 0.000 0.000 1 0.000 0.000 0.000 ~ ~ ~ 0 0 0 1 0 force

function ./guardian_ghast/particle/yellow:
    particle dust_color_transition 1.000 1.000 0.000 1 0.000 0.000 0.000 ~3 ~5 ~ 0 0 1.5 0.01 10 force
    particle dust_color_transition 1.000 1.000 0.000 1 0.000 0.000 0.000 ~-3 ~5 ~ 0 0 1.5 0.01 10 force
    particle dust_color_transition 1.000 1.000 0.000 1 0.000 0.000 0.000 ~ ~5 ~3 1.5 0 0 0.01 10 force
    particle dust_color_transition 1.000 1.000 0.000 1 0.000 0.000 0.000 ~ ~5 ~-3 1.5 0 0 0.01 10 force
    # positioned ^1.5 ^2.5 ^2.5 run particle dust_color_transition 1.000 1.000 0.000 1 0.000 0.000 0.000 ~ ~ ~ 0 0 0 1 0 force
    # positioned ^-1.5 ^2.5 ^2.5 run particle dust_color_transition 1.000 1.000 0.000 1 0.000 0.000 0.000 ~ ~ ~ 0 0 0 1 0 force

function ./guardian_ghast/awoken:
    scoreboard players reset @s dnl.timer
    scoreboard players set @s dnl.health 12
    data merge entity @s{Invulnerable:0b,NoAI:0b,Silent:0b}
    tag @s remove dnl.guardian_ghast.awoken
    tag @s remove dnl.guardian_ghast.stunned
    tag @s remove dnl.guardian_ghast.stunned_long

function ./guardian_ghast/sleep:
    tag @s remove dnl.guardian_ghast.red
    tag @s remove dnl.guardian_ghast.yellow
    tag @s remove dnl.guardian_ghast.blue
    tag @s remove dnl.guardian_ghast.sleep
    tag @s remove dnl.guardian_ghast.stunned
    tag @s remove dnl.guardian_ghast.stunned_long
    tag @s remove dnl.guardian_ghast.attacker
    scoreboard players set @s dnl.phase 0
    scoreboard players set @s dnl.health 12
    scoreboard players reset @s dnl.timer
    data merge entity @s{Invulnerable:1b,NoAI:1b,Silent:1b}

function ./guardian_ghast/stunned:
    scoreboard players add @s dnl.timer 1
    if score @s dnl.timer matches 1 run function ./guardian_ghast/stunned/init:
        playsound minecraft:entity.wither.hurt master @a ~ ~ ~ 4 0.1
        data merge entity @s {Invulnerable:1b,NoAI:1b,Silent:1b}
        scoreboard players operation #dnl.guardian_ghast.lid dnl.lid = @s dnl.lid
        as @e[type=marker,tag=dnl.core,tag=dnl.guardian_ghast] if score @s dnl.lid = #dnl.guardian_ghast.lid dnl.lid run tag @s add dnl.guardian_ghast.core.stunned
    if score @s dnl.timer matches 1.. run function ./guardian_ghast/stunned/particles:
        particle minecraft:reverse_portal ~ ~ ~ 3 3 3 0.01 20 force
        particle minecraft:soul ~ ~ ~ 3 3 3 0.01 10 force
    if score @s dnl.timer matches 160.. run function ./guardian_ghast/stunned/end: 
        playsound minecraft:entity.ghast.warn master @a ~ ~ ~ 4 0.8
        playsound minecraft:entity.lightning_bolt.thunder master @a ~ ~ ~ 4 1
        data merge entity @s{Invulnerable:0b,NoAI:0b,Silent:0b}
        scoreboard players set @s dnl.health 12
        tag @s remove dnl.guardian_ghast.stunned
        scoreboard players reset @s dnl.timer

function ./guardian_ghast/stunned_long:
    unless score @s dnl.timer matches 2.. run scoreboard players add @s dnl.timer 1
    if score @s dnl.timer matches 1 run function ./guardian_ghast/stunned_long/init:
        playsound minecraft:entity.wither.hurt master @a ~ ~ ~ 4 0.1
        data merge entity @s {Invulnerable:1b,NoAI:1b,Silent:1b}
    if score @s dnl.timer matches 1.. run function ./guardian_ghast/stunned_long/particles:
        particle minecraft:reverse_portal ~ ~ ~ 3 3 3 0.01 20 force
        particle minecraft:soul ~ ~ ~ 3 3 3 0.01 10 force

function ./guardian_ghast/attacker:
    scoreboard players add @s dnl.timer 1
    if score @s dnl.timer matches 5 run function ./guardian_ghast/attacker/teleport
    if score @s dnl.timer matches 10 run function ./guardian_ghast/attacker/teleport
    if score @s dnl.timer matches 15 run function ./guardian_ghast/attacker/teleport
    if score @s dnl.timer matches 20 run function ./guardian_ghast/attacker/teleport

    if score @s dnl.timer matches 155 run function ./guardian_ghast/attacker/teleport
    if score @s dnl.timer matches 160 run function ./guardian_ghast/attacker/teleport
    if score @s dnl.timer matches 165 run function ./guardian_ghast/attacker/teleport
    if score @s dnl.timer matches 170 run function ./guardian_ghast/attacker/teleport

    if score @s dnl.timer matches 175 if score @s dnl.phase matches 5 run function ./guardian_ghast/attacker/ability_activation

    if score @s dnl.timer matches 175.. if entity @s[tag=dnl.guardian_ghast.ability,tag=dnl.guardian_ghast.red] run function ./guardian_ghast/ability_red/main
    if score @s dnl.timer matches 175.. if entity @s[tag=dnl.guardian_ghast.ability,tag=dnl.guardian_ghast.yellow] run function ./guardian_ghast/ability_yellow/main
    if score @s dnl.timer matches 175.. if entity @s[tag=dnl.guardian_ghast.ability,tag=dnl.guardian_ghast.blue] run function ./guardian_ghast/ability_blue/main

    if score @s dnl.timer matches 300 run function ./guardian_ghast/attacker/retreat

function ./guardian_ghast/attacker/ability_activation:
    scoreboard players reset #dnl.guardian_ghast.ability_activate dnl.boolean
    rng 1 3
    outcome 1..2:
        scoreboard players set #dnl.guardian_ghast.ability_activate dnl.boolean 1
    
    if score #dnl.guardian_ghast.ability_activate dnl.boolean matches 1 run function ./guardian_ghast/attacker/ability_successful:
        tag @s add dnl.this
        scoreboard players operation #dnl.guardian_ghast.sid dnl.sid = @s dnl.sid
        as @e[type=marker,tag=dnl.sisters_of_sorrow] if score @s dnl.sid = #dnl.guardian_ghast.sid dnl.sid at @s at @a[distance=..64,limit=1,sort=random] run tp @e[tag=dnl.this] ~ 260 ~
        tag @s remove dnl.this
        playsound minecraft:entity.ghast.warn master @a ~ ~ ~ 4 0.8
        playsound minecraft:entity.lightning_bolt.thunder master @a ~ ~ ~ 4 1
        tag @s add dnl.guardian_ghast.ability


function ./guardian_ghast/attacker/retreat:
    playsound minecraft:entity.enderman.teleport master @a ~ ~ ~ 4 1.5
    particle minecraft:reverse_portal ~ ~ ~ 3 3 3 0.01 20 force
    tag @s add dnl.this
    scoreboard players operation #dnl.guardian_ghast.lid dnl.lid = @s dnl.lid
    as @e[type=marker,tag=dnl.guardian_ghast,tag=dnl.core] if score @s dnl.lid = #dnl.guardian_ghast.lid dnl.lid at @s run tp @e[tag=dnl.this] ~ ~ ~
    tag @s remove dnl.this
    scoreboard players reset @s dnl.timer
    tag @s remove dnl.guardian_ghast.ability
    tag @s remove dnl.guardian_ghast.attacker


function ./guardian_ghast/attacker/teleport:
    playsound minecraft:entity.enderman.teleport master @a ~ ~ ~ 4 1.5
    particle minecraft:flash ~ ~ ~ 0 0 0 0.01 1 force

    scoreboard players operation #dnl.guardian_ghast.sid dnl.sid = @s dnl.sid
    as @e[type=marker,tag=dnl.sisters_of_sorrow] if score @s dnl.sid = #dnl.guardian_ghast.sid dnl.sid at @s run summon marker ~ ~ ~ {Tags:["dnl.marker","dnl.guardian_ghast.teleporter"]}
    as @e[type=marker,tag=dnl.guardian_ghast.teleporter] at @s run spreadplayers ~ ~ 3 15 true @s

    rng 1 4
    outcome 1:
        at @e[type=marker,tag=dnl.guardian_ghast.teleporter] run tp @s ~ 230 ~
    outcome 2:
        at @e[type=marker,tag=dnl.guardian_ghast.teleporter] run tp @s ~ 235 ~
    outcome 3:
        at @e[type=marker,tag=dnl.guardian_ghast.teleporter] run tp @s ~ 240 ~
    outcome 4:
        at @e[type=marker,tag=dnl.guardian_ghast.teleporter] run tp @s ~ 245 ~
    
    kill @e[type=marker,tag=dnl.guardian_ghast.teleporter]
    
function ./guardian_ghast/ability_red/main:
    for i in range(26):
        j = i * 3 + 175
        if score @s dnl.timer matches j run function ./guardian_ghast/ability_red/fireball_spawn

function ./guardian_ghast/ability_red/fireball_spawn:
    summon minecraft:marker ~ ~ ~ {Tags:["dnl.marker","dnl.guardian_ghast.fireball_spawner"]}
    as @e[type=marker,tag=dnl.guardian_ghast.fireball_spawner] at @s run function ./guardian_ghast/ability_red/fireball_type:
        spreadplayers ~ ~ 1 10 true @s
        rng 1 4
        outcome 1..2:
            at @s run summon small_fireball ~ 250 ~ {direction:[0.0,-0.1,0.0],power:[0.0,-0.1,0.0]}
        outcome 3..4:
            at @s run summon fireball ~ 250 ~ {ExplosionPower:3,direction:[0.0,-0.1,0.0],power:[0.0,-0.1,0.0]}
        kill @s

function ./guardian_ghast/ability_yellow/main:
    for i in range(3):
        a = i * 5 + 175
        b = i * 5 + 200
        c = i * 5 + 235
        if score @s dnl.timer matches a run function ./guardian_ghast/ability_yellow/thunder_spawn
        if score @s dnl.timer matches b run function ./guardian_ghast/ability_yellow/thunder_spawn
        if score @s dnl.timer matches c run function ./guardian_ghast/ability_yellow/thunder_spawn
        scoreboard players operation #dnl.guardian_ghast.sid dnl.sid = @s dnl.sid
        as @e[type=marker,tag=dnl.sisters_of_sorrow] if score @s dnl.sid = #dnl.guardian_ghast.sid dnl.sid at @s as @a[distance=..64] at @s if predicate ./guardian_ghast/player_flying run function ./guardian_ghast/ability_yellow/thunder_on_flying_player:
            if score @s dnl.timer matches 175..235 run particle totem_of_undying ^ ^ ^2 2 2 2 0.01 20 force
            if score @s dnl.timer matches 243..245 run summon lightning_bolt ~ ~ ~1
            if score @s dnl.timer matches 243..245 run summon lightning_bolt ~1 ~ ~
            if score @s dnl.timer matches 243..245 run summon lightning_bolt ~ ~ ~-1
            if score @s dnl.timer matches 243..245 run summon lightning_bolt ~-1 ~ ~
            if score @s dnl.timer matches 243..245 run effect give @s minecraft:instant_damage 1 1

function ./guardian_ghast/ability_yellow/thunder_spawn:
    summon minecraft:marker ~ ~ ~ {Tags:["dnl.marker","dnl.guardian_ghast.thunder_spawner"]}
    as @e[type=marker,tag=dnl.guardian_ghast.thunder_spawner] at @s run function ./guardian_ghast/ability_yellow/thunder_drop:
        spreadplayers ~ ~ 1 10 true @s
        at @s run summon minecraft:lightning_bolt ~ ~ ~
        at @s if entity @a[distance=..10] run function ./guardian_ghast/ability_yellow/mob_spawn
        kill @s

function ./guardian_ghast/ability_yellow/mob_spawn:
    rng 1 4
    outcome 1:
        summon creeper ~ ~ ~ {
            DeathLootTable:"minecraft:empty",
            powered:1b,
            Tags:["dnl.mob","dnl.guardian_ghast.minion","dnl.new"]
        }
    outcome 2:
        summon witch ~ ~ ~ {
            DeathLootTable:"minecraft:empty",
            Tags:["dnl.mob","dnl.guardian_ghast.minion","dnl.new"]
        }
    outcome 3:
        summon skeleton ~ ~ ~ {
            DeathLootTable:"minecraft:empty",
            Tags:["dnl.mob","dnl.guardian_ghast.minion","dnl.new"],
            HandItems:[{id:"minecraft:bow",Count:1b,tag:{Enchantments:[{id:"minecraft:power",lvl:5s},{id:"minecraft:vanishing_curse",lvl:1s}]}},{}],
            HandDropChances:[0.000F,0.000F],
            ArmorItems:[{},{},{},{id:"minecraft:iron_helmet",Count:1b,tag:{Enchantments:[{id:"minecraft:blast_protection",lvl:5s},{id:"minecraft:vanishing_curse",lvl:1s}]}}],
            ArmorDropChances:[0.000F,0.000F,0.000F,0.000F]
        }
    outcome 4:
        summon skeleton ~ ~ ~ {
            DeathLootTable:"minecraft:empty",
            Tags:["dnl.mob","dnl.guardian_ghast.minion","dnl.new"],
            HandItems:[{id:"minecraft:iron_sword",Count:1b,tag:{Enchantments:[{id:"minecraft:sharpness",lvl:5s},{id:"minecraft:vanishing_curse",lvl:1s}]}},{}],
            HandDropChances:[0.000F,0.000F],
            ArmorItems:[{},{},{},{id:"minecraft:iron_helmet",Count:1b,tag:{Enchantments:[{id:"minecraft:blast_protection",lvl:5s},{id:"minecraft:vanishing_curse",lvl:1s}]}}],
            ArmorDropChances:[0.000F,0.000F,0.000F,0.000F]
        }
    scoreboard players operation @e[tag=dnl.new] dnl.sid = #dnl.guardian_ghast.sid dnl.sid
    tag @e remove dnl.new

predicate ./guardian_ghast/player_flying {
    "condition": "minecraft:entity_properties",
    "entity": "this",
    "predicate": {
        "nbt": "{FallFlying:1b}"
    }
}

function ./guardian_ghast/ability_blue/main:
    for i in range(26):
        j = i * 3 + 175
        if score @s dnl.timer matches j run function ./guardian_ghast/ability_blue/ice_spawn

function ./guardian_ghast/ability_blue/ice_spawn:
    summon minecraft:marker ~ ~ ~ {Tags:["dnl.marker","dnl.guardian_ghast.ice_spawner"]}
    as @e[type=marker,tag=dnl.guardian_ghast.ice_spawner] at @s run function ./guardian_ghast/ability_blue/ice_drop:
        spreadplayers ~ ~ 1 10 true @s
        at @s run summon snowball ~ 250 ~ {Tags:["dnl.snowball","dnl.raining_soul"],Item:{id:"minecraft:blue_ice",Count:1b}}
        kill @s

function ./guardian_ghast/hurt:
    if entity @s[advancements={dnl:entity/guardian_ghast/hurt={by_arrow=true}}] run scoreboard players set #dnl.guardian_ghast.damage dnl.int 4
    if entity @s[advancements={dnl:entity/guardian_ghast/hurt={by_fireball=true}}] run scoreboard players set #dnl.guardian_ghast.damage dnl.int 12
    if entity @s[advancements={dnl:entity/guardian_ghast/hurt={by_firework=true}}] run scoreboard players set #dnl.guardian_ghast.damage dnl.int 12
    if entity @s[advancements={dnl:entity/guardian_ghast/hurt={by_trident=true}}] run scoreboard players set #dnl.guardian_ghast.damage dnl.int 8
    if entity @s[advancements={dnl:entity/guardian_ghast/hurt={not_projectile=true}}] run scoreboard players set #dnl.guardian_ghast.damage dnl.int 1
    advancement revoke @s only dnl:entity/guardian_ghast/hurt
    as @e[type=ghast,tag=dnl.guardian_ghast,nbt={HurtTime:10s}] run function ./guardian_ghast/health_calculation:
        scoreboard players operation @s dnl.health -= #dnl.guardian_ghast.damage dnl.int
        if score @s dnl.health matches ..0 if score @s dnl.phase matches ..1 unless entity @s[tag=dnl.guardian_ghast.stunned_long] run function ./guardian_ghast/hurt/stunned_long
        if score @s dnl.health matches ..0 if score @s dnl.phase matches 3.. if entity @s[tag=dnl.guardian_ghast.attacker] run function ./guardian_ghast/attacker/retreat
        if score @s dnl.health matches ..0 if score @s dnl.phase matches 2.. unless entity @s[tag=dnl.guardian_ghast.stunned] run tag @s add dnl.guardian_ghast.stunned

function ./guardian_ghast/hurt/stunned_long:
    tag @s add dnl.guardian_ghast.stunned_long
    scoreboard players operation #dnl.guardian_ghast.sid dnl.sid = @s dnl.sid
    as @e[type=marker,tag=dnl.sisters_of_sorrow] if score @s dnl.sid = #dnl.guardian_ghast.sid dnl.sid run scoreboard players remove @s dnl.health 4

function ./guardian_ghast/death:
    as @e[type=marker,tag=dnl.core,tag=dnl.guardian_ghast] if score @s dnl.lid = #dnl.death_entity_lid dnl.lid run kill @s

advancement ./guardian_ghast/hurt {
    "criteria": {
        "hurt_guardian_ghast": {
            "trigger": "minecraft:player_hurt_entity",
            "conditions": {
                "entity": {
                    "type": "minecraft:ghast",
                    "nbt": "{Tags:[\"dnl.guardian_ghast\"]}"
                }
            }
        },
        "by_fireball": {
            "trigger": "minecraft:player_hurt_entity",
            "conditions": {
                "damage": {
                    "type": {
                        "is_projectile": true,
                        "direct_entity": {
                            "type": "minecraft:fireball"
                        }
                    }
                }
            }
        },
        "by_arrow": {
            "trigger": "minecraft:player_hurt_entity",
            "conditions": {
                "damage": {
                    "type": {
                        "is_projectile": true,
                        "direct_entity": {
                            "type": "#minecraft:arrows"
                        }
                    }
                }
            }
        },
        "by_firework": {
            "trigger": "minecraft:player_hurt_entity",
            "conditions": {
                "damage": {
                    "type": {
                        "is_projectile": true,
                        "direct_entity": {
                            "type": "minecraft:firework_rocket"
                        }
                    }
                }
            }
        },
        "by_trident": {
            "trigger": "minecraft:player_hurt_entity",
            "conditions": {
                "damage": {
                    "type": {
                        "is_projectile": true,
                        "direct_entity": {
                            "type": "minecraft:trident"
                        }
                    }
                }
            }
        },
        "not_projectile": {
            "trigger": "minecraft:player_hurt_entity",
            "conditions": {
                "damage": {
                    "type": {
                        "is_projectile": false
                    }
                }
            }
        }
    },
    "requirements": [
        [
            "hurt_guardian_ghast"
        ],
        [
            "by_fireball",
            "by_arrow",
            "by_firework",
            "by_trident",
            "not_projectile"
        ]
    ],
    "rewards": {
        "function": "dnl:entity/guardian_ghast/hurt"
    }
}

