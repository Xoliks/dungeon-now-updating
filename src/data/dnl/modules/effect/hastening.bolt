from ../api import
    on_entity_effect

from reapermc:api import
    listener
    on_server_load

@listener(on_entity_effect)
def entity_effect():
    if entity @s[tag=dnl.hastening] run function ./hastening/main

@listener(on_server_load)
def load():
    scoreboard objectives add dnl.timer.hastening dummy

function ./hastening/apply:
    unless entity @s[tag=dnl.hastening] run scoreboard players add @s dnl.effect 1
    function ./hastening/increase
    tag @s add dnl.hastening


function ./hastening/remove:
    scoreboard players remove @s dnl.effect 1
    attribute @s minecraft:generic.attack_speed base set 4
    tag @s remove dnl.hastening
    scoreboard players reset @s dnl.timer.hastening

function ./hastening/main:
    scoreboard players add @s dnl.timer.hastening 1
    particle minecraft:scrape ~ ~ ~ 0.5 1 0.5 0.01 1 normal
    if score @s dnl.timer.hastening matches 200 run function ./hastening/remove

function ./hastening/increase:
    store result score #dnl.attack_speed dnl.int run attribute @s minecraft:generic.attack_speed base get 10
    if score #dnl.attack_speed dnl.int matches 40 run function ./hastening/boost_1:
        attribute @s minecraft:generic.attack_speed base set 4.6
        playsound minecraft:entity.player.attack.sweep master @s ~ ~ ~ 1 1 1
    if score #dnl.attack_speed dnl.int matches 46 run function ./hastening/boost_2:
        attribute @s minecraft:generic.attack_speed base set 5.2
        playsound minecraft:entity.player.attack.sweep master @s ~ ~ ~ 1 1.3 1
    if score #dnl.attack_speed dnl.int matches 52 run function ./hastening/boost_3:
        attribute @s minecraft:generic.attack_speed base set 5.8
        playsound minecraft:entity.player.attack.sweep master @s ~ ~ ~ 1 1.6 1
    if score #dnl.attack_speed dnl.int matches 58 run function ./hastening/boost_4:
        attribute @s minecraft:generic.attack_speed base set 6.4
        playsound minecraft:entity.player.attack.sweep master @s ~ ~ ~ 1 1.7 1
    if score #dnl.attack_speed dnl.int matches 64 run function ./hastening/boost_5:
        playsound minecraft:entity.player.attack.strong master @s ~ ~ ~ 1 0.1 1
        playsound minecraft:entity.player.attack.sweep master @s ~ ~ ~ 1 2.0 1